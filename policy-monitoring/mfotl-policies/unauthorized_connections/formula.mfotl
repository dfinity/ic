(*
  Policy: No node which participates in a subnet logs unauthorized TLS
  connection attempts (except for connection attempts to nodes in other
  subnets, e.g., soon after a destination node has been reassigned)

  Relevant events
  ---------------
  - ControlPlane__spawn_accept_task__tls_server_handshake_failed(
      local_addr:string,
      peer_addr:string
    )
    Warn "ControlPlane::spawn_accept_task(): tls_server_handshake failed: error = UnauthenticatedClient, local_addr = {:?}, peer_addr = {:?}"

  // The following events should be used to detect nodes that are being reassigned
  - originally_in_subnet(node_id:string, node_addr:string, subnet_id:string)
  - registry__node_removed_from_subnet(node_id:string, node_addr:string)
  - registry__node_added_to_subnet(node_id:string, node_addr:string, subnet_id:string)
*)

LET unauthorized_connection_attempt(local_addr, peer_addr) =
  ControlPlane__spawn_accept_task__tls_server_handshake_failed(local_addr, peer_addr)
IN
LET node_added_to_subnet(node_id, node_addr, subnet) =
  registry__node_added_to_subnet(node_id, node_addr, subnet)
IN
LET node_removed_from_subnet(node_id, node_addr) =
  registry__node_removed_from_subnet(node_id, node_addr)
IN
LET in_subnet(node_id, node_addr, subnet) =
  ((*
      The node was originally in this subnet, and it has not been unassigned from it.
    *)
    (ONCE originally_in_subnet(node_id, node_addr, subnet)) AND NOT (
      ONCE node_removed_from_subnet(node_id, node_addr)
    )
  )
  OR
  ((*
      The node was possibly not originally in this subnet, but it has been assigned to it.
    *)
    (
      NOT node_removed_from_subnet(node_id, node_addr)
      SINCE node_added_to_subnet(node_id, node_addr, subnet)
    )
  )
IN
in_subnet(dest_id, dest_addr, subnet) AND in_subnet(sender_id, sender_addr, subnet)
AND
unauthorized_connection_attempt(sender_addr, dest_addr)