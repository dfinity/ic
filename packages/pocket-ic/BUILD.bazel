load("@rules_rust//rust:defs.bzl", "rust_library", "rust_test", "rust_test_suite")
load("//bazel:canisters.bzl", "rust_canister")

package(default_visibility = ["//visibility:public"])

DEPENDENCIES = [
    # Keep sorted.
    "@crate_index//:base64",
    "@crate_index//:candid",
    "@crate_index//:hex",
    "@crate_index//:ic-cdk",
    "@crate_index//:reqwest",
    "@crate_index//:schemars",
    "@crate_index//:serde",
    "@crate_index//:serde_bytes",
    "@crate_index//:serde_json",
    "@crate_index//:sha2",
    "@crate_index//:tokio",
    "@crate_index//:tracing",
    "@crate_index//:tracing-appender",
    "@crate_index//:tracing-subscriber",
]

MACRO_DEPENDENCIES = []

TEST_DEPENDENCIES = [
    # Keep sorted.
    "//rs/rosetta-api/icp_ledger",
    "//rs/test_utilities/load_wasm",
    "//rs/types/base_types",
    "//rs/universal_canister/lib",
    "@crate_index//:candid_parser",
    "@crate_index//:flate2",
    "@crate_index//:k256",
    "@crate_index//:lazy_static",
    "@crate_index//:wat",
]

rust_library(
    name = "pocket-ic",
    srcs = glob(["src/**/*.rs"]),
    proc_macro_deps = MACRO_DEPENDENCIES,
    version = "3.1.0",
    deps = DEPENDENCIES,
)

rust_canister(
    name = "test_canister",
    srcs = ["tests/test_canister.rs"],
    service_file = ":tests/test_canister.did",
    deps = [
        # Keep sorted.
        "@crate_index//:candid",
        "@crate_index//:ic-cdk",
    ],
)

rust_test(
    name = "test_canister_unit_test",
    crate = ":_wasm_test_canister",
    data = ["tests/test_canister.did"],
    env = {"CARGO_MANIFEST_DIR": "packages/pocket-ic"},
    deps = [":pocket-ic"] + DEPENDENCIES + TEST_DEPENDENCIES,
)

rust_test_suite(
    name = "test",
    size = "small",
    srcs = ["tests/tests.rs"],
    data = [
        "tests/counter.wasm",
        "tests/icp_ledger.wasm",
        ":test_canister.wasm",
        "//rs/pocket_ic_server:pocket-ic-server",
    ],
    env = {
        "POCKET_IC_BIN": "$(rootpath //rs/pocket_ic_server:pocket-ic-server)",
        "COUNTER_WASM": "packages/pocket-ic/tests/counter.wasm",
        "LEDGER_WASM": "packages/pocket-ic/tests/icp_ledger.wasm",
        "TEST_WASM": "$(rootpath :test_canister.wasm)",
    },
    flaky = False,
    proc_macro_deps = MACRO_DEPENDENCIES,
    deps = [":pocket-ic"] + DEPENDENCIES + TEST_DEPENDENCIES,
)

rust_test_suite(
    name = "slow",
    size = "medium",
    srcs = ["tests/slow.rs"],
    data = [
        "//rs/pocket_ic_server:pocket-ic-server",
    ],
    env = {
        "POCKET_IC_BIN": "$(rootpath //rs/pocket_ic_server:pocket-ic-server)",
    },
    flaky = False,
    proc_macro_deps = MACRO_DEPENDENCIES,
    tags = ["cpu:8"],
    deps = [":pocket-ic"] + DEPENDENCIES + TEST_DEPENDENCIES,
)
