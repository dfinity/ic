load("//rs/tests:common.bzl", "MAINNET_LATEST_GUESTOS_REVISION", "MAINNET_LATEST_HOSTOS_REVISION")

package(default_visibility = ["__subpackages__"])

exports_files([
    "partitions.csv",
    "grub.cfg",
    "config/config.ini",
    "config/node_operator_private_key.pem",
    "config/ssh_authorized_keys/admin",
    "data/deployment.json.template",
    "data/nns_public_key.pem",
])

# All setup-os targets are named the same, just stay in different submodules.
# To build or run specific target:
#
# bazel run   //ic-os/setupos/envs/prod:upload_disk-img           [--s3_endpoint=<internal endpoint address>]
# bazel build //ic-os/setupos/envs/dev/...
#
# check //ic-os/defs.bzl for the full list of targets.

# Postprocess SetupOS dev into a "test" image for use in nested tests
genrule(
    name = "test-img",
    srcs = ["//ic-os/setupos/envs/dev:disk-img.tar.zst"],
    outs = ["test-img.tar.zst"],
    cmd = """
        tar -xf $<
        $(location //rs/ic_os/dev_test_tools/setupos-disable-checks) --image-path disk.img
        tar --zstd -Scf $@ disk.img
    """,
    tags = [
        "manual",
        "no-cache",
    ],
    target_compatible_with = ["@platforms//os:linux"],
    tools = ["//rs/ic_os/dev_test_tools/setupos-disable-checks"],
    visibility = [
        "//rs:ic-os-pkg",
        "//rs:system-tests-pkg",
    ],
)

# Postprocess SetupOS mainnet into a "test" image for use in nested tests
genrule(
    name = "mainnet-latest-test-img",
    srcs = ["@mainnet_latest_setupos_disk_image//file"],
    outs = ["mainnet-latest-test-img.tar.zst"],
    cmd = """
        tar -xf $<
        $(location //rs/ic_os/dev_test_tools/setupos-disable-checks) --image-path disk.img
        tar --zstd -Scf $@ disk.img
    """,
    tags = ["manual"],
    target_compatible_with = ["@platforms//os:linux"],
    tools = ["//rs/ic_os/dev_test_tools/setupos-disable-checks"],
    visibility = [
        "//rs:ic-os-pkg",
        "//rs:system-tests-pkg",
    ],
)

# Extract the GuestOS image from SetupOS
genrule(
    name = "mainnet-latest-guest-img",
    srcs = ["@mainnet_latest_setupos_disk_image//file"],
    outs = ["mainnet-latest-guest-img.tar.zst"],
    cmd = """#!/bin/bash
        if [ "{guestos_version}" != "{setupos_version}" ]; then
            echo "FAIL: We assume the latest HostOS and GuestOS releases are in line for this to work, and they are not."
            exit 1
        fi
        $(location //rs/ic_os/build_tools/partition_tools:extract-guestos) --image $< $@
    """.format(
        guestos_version = MAINNET_LATEST_GUESTOS_REVISION,
        setupos_version = MAINNET_LATEST_HOSTOS_REVISION,
    ),
    tags = ["manual"],
    target_compatible_with = ["@platforms//os:linux"],
    tools = ["//rs/ic_os/build_tools/partition_tools:extract-guestos"],
    visibility = [
        "//rs:ic-os-pkg",
        "//rs:system-tests-pkg",
    ],
)

# Extract the GuestOS image from SetupOS
genrule(
    name = "mainnet-nns-guest-img",
    srcs = ["@mainnet_nns_setupos_disk_image//file"],
    outs = ["mainnet-nns-guest-img.tar.zst"],
    cmd = """
        $(location //rs/ic_os/build_tools/partition_tools:extract-guestos) --image $< $@
    """,
    tags = ["manual"],
    target_compatible_with = ["@platforms//os:linux"],
    tools = ["//rs/ic_os/build_tools/partition_tools:extract-guestos"],
    visibility = [
        "//rs:ic-os-pkg",
        "//rs:system-tests-pkg",
    ],
)
