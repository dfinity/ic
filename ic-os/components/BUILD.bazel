load("//bazel:defs.bzl", "write_info_file_var")
load("//ic-os/components/conformance_tests:defs.bzl", "check_unused_components_test")
load("guestos.bzl", guestos_component_files = "component_files")
load("hostos.bzl", hostos_component_files = "component_files")
load("setupos.bzl", setupos_component_files = "component_files")

package(default_visibility = ["//ic-os:__subpackages__"])

PUBLIC_GUESTOS_EXPORTS = [
    "ic/generate-ic-config/ic.json5.template",
    "networking/dev-certs/canister_http_test_ca.cert",
    "networking/dev-certs/canister_http_test_ca.key",
]

exports_files(PUBLIC_GUESTOS_EXPORTS)

exports_files(
    glob(
        ["**"],
        exclude = PUBLIC_GUESTOS_EXPORTS,
    ),
    visibility = [
        "//ic-os/guestos:__subpackages__",
        "//ic-os/hostos:__subpackages__",
        "//ic-os/setupos:__subpackages__",
    ],
)

write_info_file_var(
    name = "commit_timestamp_txt",
    varname = "STABLE_COMMIT_TIMESTAMP",
)

used_components = guestos_component_files | hostos_component_files | setupos_component_files

REPO_COMPONENTS = glob(
    ["**/*"],
    exclude = [
        "**/BUILD.bazel",
        "**/*.bzl",
        "**/*.md",
        "**/*.adoc",
    ],
)

# files used for testing and development that aren't "used" by any ic-os variant
ignored_repo_components = [
    "networking/dev-certs/canister_http_test_ca.key",
    "networking/dev-certs/root_cert_gen.sh",
    "misc/guestos-recovery/guestos-recovery-engine/guestos-recovery-engine.sh",
    "misc/guestos-recovery/guestos-recovery-engine/guestos-recovery-engine.service",
    "selinux/guestos-recovery-engine/guestos-recovery-engine.fc",
    "selinux/guestos-recovery-engine/guestos-recovery-engine.te",
]

check_unused_components_test(
    name = "check_unused_components_test",
    ignored_repo_components = ignored_repo_components,
    repo_components = REPO_COMPONENTS,
    used_components = used_components.keys(),
)

genrule(
    name = "validate-ic-json5-script",
    testonly = True,
    outs = ["validate-ic-json5.sh"],
    cmd = """
    cat <<EOF > $@
#!/bin/bash
set -eux
TEMPFILE=\\$$(mktemp)
# The version of 'json5' available on CI containers does not support
# the '--validate' argument so we have to copy to a temporary file
# and compile in place with the '-c' argument to check validity.
sed <"\\$${1?missing json5 file}" 's/{{[^}]*}}/0/g' >"\\$$TEMPFILE"
echo "Validating: \\$$1"
json5 -c "\\$$TEMPFILE"
rm "\\$$TEMPFILE"
EOF
    """,
    tags = ["manual"],
)

sh_test(
    name = "validate-ic-json5",
    srcs = ["validate-ic-json5-script"],
    args = ["$(execpath ic/generate-ic-config/ic.json5.template)"],
    data = ["ic/generate-ic-config/ic.json5.template"],
)
