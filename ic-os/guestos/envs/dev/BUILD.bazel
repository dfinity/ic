load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("//bazel:defs.bzl", "file_size_check")
load("//bazel:mainnet-icos-images.bzl", "MAINNET_LATEST")
load("//ic-os:defs.bzl", "extract_mainnet_guestos_image", "icos_build")
load("//ic-os/guestos:defs.bzl", "image_deps")
load("//publish:defs.bzl", "artifact_bundle")

# The macro contains several targets.
# Check
#       //ic-os/guestos/BUILD.bazel for examples
#    or //ic-os/defs.bzl for the full list of targets.
icos_images = icos_build(
    name = "dev",
    image_deps_func = image_deps,
    visibility = [
        "//rs:ic-os-pkg",
        "//rs:system-tests-pkg",
        "//testnet/tools:icos_deploy-pkg",
    ],
)

file_size_check(
    name = "disk_img_size_check",
    file = icos_images.disk_image,
    max_file_size = 600 * 1000 * 1000,  # 580 MB on 2025-06-26
)

file_size_check(
    name = "update_img_size_check",
    file = icos_images.update_image,
    max_file_size = 600 * 1000 * 1000,  # 578 MB on 2025-06-26
)

file_size_check(
    name = "update_img_test_size_check",
    file = icos_images.update_image_test,
    max_file_size = 600 * 1000 * 1000,  # 578 MB on 2025-06-26
)

# The icos_deploy testnet script needs this
artifact_bundle(
    name = "bundle-disk",
    testonly = True,
    inputs = [
        icos_images.disk_image,
        icos_images.launch_measurements,
    ],
    prefix = "guest-os/disk-img-dev",
    visibility = ["//visibility:public"],
)

# Export checksums & build artifacts for update images
artifact_bundle(
    name = "bundle-update",
    testonly = True,
    inputs = [
        icos_images.update_image,
        icos_images.update_image_test,
        icos_images.launch_measurements,
    ],
    prefix = "guest-os/update-img-dev",
    visibility = ["//visibility:public"],
)

extract_mainnet_guestos_image(
    name = "mainnet_latest_guest_img",
    disk_img = "@mainnet_latest_disk_img_dev//file",
    visibility = ["//rs/tests:__subpackages__"],
)

write_file(
    name = "mainnet_latest_launch_measurements",
    out = "mainnet_latest_launch_measurements.json",
    content = [json.encode(MAINNET_LATEST["dev_launch_measurements"])],
    visibility = ["//rs/tests:__subpackages__"],
)
