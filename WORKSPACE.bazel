workspace(
    name = "ic",
    managed_directories = {"@sw_npm": ["node_modules"]},
)

load("//third_party/lmdb-0.9.70:repository.bzl", "lmdb_repository")
load("//third_party/lmdb-rkv-0.14.99:repository.bzl", "lmdb_rkv_repository")
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive", "http_file", "http_jar")
load("@bazel_tools//tools/build_defs/repo:git.bzl", "git_repository", "new_git_repository")

http_archive(
    name = "io_bazel_rules_go",
    sha256 = "099a9fb96a376ccbbb7d291ed4ecbdfd42f6bc822ab77ae6f1b5cb9e914e94fa",
    urls = [
        "https://mirror.bazel.build/github.com/bazelbuild/rules_go/releases/download/v0.35.0/rules_go-v0.35.0.zip",
        "https://github.com/bazelbuild/rules_go/releases/download/v0.35.0/rules_go-v0.35.0.zip",
    ],
)

http_archive(
    name = "bazel_gazelle",
    sha256 = "448e37e0dbf61d6fa8f00aaa12d191745e14f07c31cabfa731f0c8e8a4f41b97",
    urls = [
        "https://mirror.bazel.build/github.com/bazelbuild/bazel-gazelle/releases/download/v0.28.0/bazel-gazelle-v0.28.0.tar.gz",
        "https://github.com/bazelbuild/bazel-gazelle/releases/download/v0.28.0/bazel-gazelle-v0.28.0.tar.gz",
    ],
)

load("@io_bazel_rules_go//go:deps.bzl", "go_register_toolchains", "go_rules_dependencies")
load("@bazel_gazelle//:deps.bzl", "gazelle_dependencies")

go_rules_dependencies()

go_register_toolchains(go_version = "1.19.3")

gazelle_dependencies(go_repository_default_config = "//:WORKSPACE.bazel")

http_archive(
    name = "bazel_skylib",
    sha256 = "f7be3474d42aae265405a592bb7da8e171919d74c16f082a5457840f06054728",
    urls = [
        "https://mirror.bazel.build/github.com/bazelbuild/bazel-skylib/releases/download/1.2.1/bazel-skylib-1.2.1.tar.gz",
        "https://github.com/bazelbuild/bazel-skylib/releases/download/1.2.1/bazel-skylib-1.2.1.tar.gz",
    ],
)

git_repository(
    name = "rules_rust",
    commit = "0ebd7aa2ed02bffb545a4980946f1ea9b5f5d347",
    remote = "https://github.com/dfinity-lab/rules_rust.git",
    shallow_since = "1679674976 +0000",
)

load("@rules_rust//rust:repositories.bzl", "rules_rust_dependencies", "rust_register_toolchains")
load("@rules_rust//tools/rust_analyzer:deps.bzl", "rust_analyzer_dependencies")

rules_rust_dependencies()

rust_analyzer_dependencies()

rust_register_toolchains(
    edition = "2021",
    # The nightly version is required to compile fuzz tests from Bazel.
    # The version below is chosen so that it is in sync with the non-nightly version.
    versions = [
        "1.66.1",
        "nightly/2022-12-21",
    ],
)

git_repository(
    name = "rules_foreign_cc",
    commit = "9acbb356916760192d4c16301a69267fe44e6dec",
    remote = "https://github.com/bazelbuild/rules_foreign_cc.git",
)

load("@rules_foreign_cc//foreign_cc:repositories.bzl", "rules_foreign_cc_dependencies")

rules_foreign_cc_dependencies()

http_archive(
    name = "rules_proto",
    sha256 = "66bfdf8782796239d3875d37e7de19b1d94301e8972b3cbd2446b332429b4df1",
    strip_prefix = "rules_proto-4.0.0",
    urls = [
        "https://mirror.bazel.build/github.com/bazelbuild/rules_proto/archive/refs/tags/4.0.0.tar.gz",
        "https://github.com/bazelbuild/rules_proto/archive/refs/tags/4.0.0.tar.gz",
    ],
)

load("@rules_proto//proto:repositories.bzl", "rules_proto_dependencies", "rules_proto_toolchains")
load("//:go_deps.bzl", "go_dependencies")

# gazelle:repository_macro go_deps.bzl%go_dependencies
go_dependencies()

rules_proto_dependencies()

rules_proto_toolchains()

load("//bazel:external_crates.bzl", "external_crates_repository")
load("//bazel/openssl_static_env:defs.bzl", "openssl_static_env")

openssl_static_env(name = "openssl_static_env")

load("@openssl_static_env//:defs.bzl", "DFINITY_OPENSSL_STATIC")

external_crates_repository(
    name = "crate_index",
    cargo_lockfile = "//:Cargo.Bazel.StaticOpenSSL.toml.lock" if DFINITY_OPENSSL_STATIC else "//:Cargo.Bazel.toml.lock",
    lockfile = "//:Cargo.Bazel.StaticOpenSSL.json.lock" if DFINITY_OPENSSL_STATIC else "//:Cargo.Bazel.json.lock",
    static_openssl = DFINITY_OPENSSL_STATIC,
)

load("@crate_index//:defs.bzl", "crate_repositories")

crate_repositories()

# Motoko support

http_archive(
    name = "rules_motoko",
    sha256 = "b08c9419b1aebd3d781a79142797df8fcf5f7187bbc266ba3a93df093636be3e",
    strip_prefix = "rules_motoko-0.2.0",
    urls = ["https://github.com/dfinity/rules_motoko/archive/refs/tags/v0.2.0.tar.gz"],
)

http_archive(
    name = "motoko_base",
    build_file_content = """
filegroup(name = "sources", srcs = glob(["*.mo"]), visibility = ["//visibility:public"])
      """,
    sha256 = "b143d641b31b13fe2d21832d7372dccb067ea1a740396e9fd50af3fe9e713247",
    strip_prefix = "motoko-base-moc-0.8.5/src",
    urls = ["https://github.com/dfinity/motoko-base/archive/refs/tags/moc-0.8.5.tar.gz"],
)

load("@rules_motoko//motoko:repositories.bzl", "rules_motoko_dependencies")

rules_motoko_dependencies()

# Support for constructing archives
http_archive(
    name = "rules_pkg",
    sha256 = "451e08a4d78988c06fa3f9306ec813b836b1d076d0f055595444ba4ff22b867f",
    urls = [
        "https://mirror.bazel.build/github.com/bazelbuild/rules_pkg/releases/download/0.7.1/rules_pkg-0.7.1.tar.gz",
        "https://github.com/bazelbuild/rules_pkg/releases/download/0.7.1/rules_pkg-0.7.1.tar.gz",
    ],
)

load("@rules_pkg//:deps.bzl", "rules_pkg_dependencies")

rules_pkg_dependencies()

# Docker container support

http_archive(
    name = "io_bazel_rules_docker",
    sha256 = "b1e80761a8a8243d03ebca8845e9cc1ba6c82ce7c5179ce2b295cd36f7e394bf",
    urls = ["https://github.com/bazelbuild/rules_docker/releases/download/v0.25.0/rules_docker-v0.25.0.tar.gz"],
)

load(
    "@io_bazel_rules_docker//toolchains/docker:toolchain.bzl",
    docker_toolchain_configure = "toolchain_configure",
)

docker_toolchain_configure(
    name = "docker_config",
    gzip_target = "@pigz",
)

load(
    "@io_bazel_rules_docker//repositories:repositories.bzl",
    container_repositories = "repositories",
)
load("//third_party/openssl:openssl_repositories.bzl", "openssl_repositories")

openssl_repositories()

load("//third_party/openssl:openssl_setup.bzl", "openssl_setup")

openssl_setup()

container_repositories()

load("@io_bazel_rules_docker//repositories:deps.bzl", container_deps = "deps")

container_deps(go_repository_default_config = "@//:WORKSPACE.bazel")

load(
    "@io_bazel_rules_docker//container:container.bzl",
    "container_pull",
)

container_pull(
    name = "static-file-server",
    digest = "sha256:c21bed6db05fe08f2da128fe96f5f5a06633348fb5bb37bf3581e6501c6b528a",
    registry = "registry.gitlab.com/dfinity-lab/open/public-docker-registry",
    repository = "halverneus/static-file-server",
)

container_pull(
    name = "bitcoind",
    digest = "sha256:17c7dd21690f3be34630db7389d2f0bff14649e27a964afef03806a6d631e0f1",
    registry = "registry.gitlab.com/dfinity-lab/open/public-docker-registry",
    repository = "kylemanna/bitcoind",
)

container_pull(
    name = "httpbin",
    digest = "sha256:506a878e386b4c3014088b9089e692ecb3cb6a35fb35c9d68c1ac300ce9ab984",
    registry = "registry.gitlab.com/dfinity-lab/open/public-docker-registry",
    repository = "dfinity/httpbin",
)

container_pull(
    name = "minica",
    digest = "sha256:c67e2c1885d438b5927176295d41aaab8a72dd9e1272ba85054bfc78191d05b0",
    registry = "registry.gitlab.com/dfinity-lab/open/public-docker-registry",
    repository = "ryantk/minica",
)

container_pull(
    name = "rust_base",
    digest = "sha256:8e94f031353596c3fc9db6a2499bcc82dacc40cb71e0703476f9fad41677efdf",
    registry = "gcr.io",
    repository = "distroless/cc-debian11",
)

container_pull(
    name = "ubuntu_base",
    digest = "sha256:965fbcae990b0467ed5657caceaec165018ef44a4d2d46c7cdea80a9dff0d1ea",
    registry = "docker.io",
    repository = "ubuntu:latest",
)

# URL: registry.gitlab.com/dfinity-lab/open/public-docker-registry/nns-dapp-specs:latest
container_pull(
    name = "nns-dapp-specs",
    digest = "sha256:9e003fe2740f2813bf9e776b9cabd5cdb1fbe15581fc4b78876708fdf3791b3f",
    registry = "registry.gitlab.com",
    repository = "dfinity-lab/open/public-docker-registry/nns-dapp-specs",
)

# Third party dependencies that require special treatment

lmdb_repository(name = "lmdb")

lmdb_rkv_repository(name = "lmdb_rkv")

http_archive(
    name = "pigz",
    build_file_content = """
cc_library(
    name = "zopfli",
    hdrs = glob(["zopfli/src/zopfli/*.h"]),
    srcs = glob(["zopfli/src/zopfli/*.c"]),
)

cc_binary(
    name = "pigz",
    srcs = [
        "pigz.c",
        "try.c",
        "try.h",
        "yarn.c",
        "yarn.h",
    ],
    linkopts = [
        "-lm",
        "-lpthread",
        "-lz",
    ],
    deps = [":zopfli"],
    visibility = ["//visibility:public"],
)""",
    sha256 = "d4396b58bc2875daf8dc3ae9a657be945c2c2697944894fea8a72634eded93ab",
    strip_prefix = "pigz-2.7",
    url = "https://github.com/madler/pigz/archive/refs/tags/v2.7.zip",
)

http_archive(
    name = "buildifier_prebuilt",
    sha256 = "0450069a99db3d414eff738dd8ad4c0969928af13dc8614adbd1c603a835caad",
    strip_prefix = "buildifier-prebuilt-0.4.0",
    urls = [
        "http://github.com/keith/buildifier-prebuilt/archive/0.4.0.tar.gz",
    ],
)

load("@buildifier_prebuilt//:deps.bzl", "buildifier_prebuilt_deps")

buildifier_prebuilt_deps()

load("@bazel_skylib//:workspace.bzl", "bazel_skylib_workspace")

bazel_skylib_workspace()

load("@buildifier_prebuilt//:defs.bzl", "buildifier_prebuilt_register_toolchains")

buildifier_prebuilt_register_toolchains()

new_git_repository(
    name = "sevtool",
    build_file = "//ic-os:sevtool/BUILD.sevtool.bazel",
    commit = "3e6418e09f5ca91d789e115d0751ead1227aab47",
    remote = "https://github.com/AMDESE/sev-tool.git",
)

# Rosetta CLI

load("//bazel:rosetta_cli.bzl", "rosetta_cli_repository")

rosetta_cli_repository(name = "rosetta-cli")

# NodeJS dependencies

http_archive(
    name = "build_bazel_rules_nodejs",
    sha256 = "f10a3a12894fc3c9bf578ee5a5691769f6805c4be84359681a785a0c12e8d2b6",
    urls = ["https://github.com/bazelbuild/rules_nodejs/releases/download/5.5.3/rules_nodejs-5.5.3.tar.gz"],
)

load("@build_bazel_rules_nodejs//:repositories.bzl", "build_bazel_rules_nodejs_dependencies")

build_bazel_rules_nodejs_dependencies()

load("@build_bazel_rules_nodejs//:index.bzl", "node_repositories", "npm_install")

node_repositories(node_version = "17.9.1")

npm_install(
    name = "sw_npm",
    package_json = "//typescript/service-worker:package.json",
    package_lock_json = "//typescript/service-worker:package-lock.json",
)

# rclone binary for upload_artifacts
load("//bazel:rclone.bzl", "rclone_repository")

rclone_repository(name = "rclone")

# trivy binary for upload_artifacts
load("//bazel:trivy.bzl", "trivy_scan")

trivy_scan(name = "trivy")

# shellcheck binary
load("//bazel:shellcheck.bzl", "shellcheck")

shellcheck(name = "shellcheck")

load("//bazel:jq.bzl", "jq_repository")

jq_repository(name = "jq")

http_archive(
    name = "aspect_bazel_lib",
    sha256 = "79381b0975ba7d2d5653239e5bab12cf54d89b10217fe771b8edd95047a2e44b",
    strip_prefix = "bazel-lib-1.12.1",
    url = "https://github.com/aspect-build/bazel-lib/archive/refs/tags/v1.12.1.tar.gz",
)

load("@aspect_bazel_lib//lib:repositories.bzl", "aspect_bazel_lib_dependencies")

aspect_bazel_lib_dependencies()

# TLA+ tools
http_jar(
    name = "tlaplus_community_modules",
    sha256 = "109e0828d192c33703d5cbc50b5b6e128acd816565616e27b3855949c7baba9c",
    url = "https://github.com/tlaplus/CommunityModules/releases/download/202302091937/CommunityModules-202302091937.jar",
)

http_jar(
    name = "tlaplus_community_modules_deps",
    sha256 = "762c4bdc25a0cb67043411c7f4f062cc2c038631c9c569539df880e0e78d5cf4",
    url = "https://github.com/tlaplus/CommunityModules/releases/download/202302091937/CommunityModules-deps-202302091937.jar",
)

# Official WebAssembly test suite.
# To be used for testing libraries that handle canister Wasm code.

new_git_repository(
    name = "wasm_spec_testsuite",
    build_file_content = """filegroup(
        name = "wast_files",
        srcs = glob(["**/*.wast"]),
        visibility = ["//visibility:public"]
    )""",
    commit = "4f77306bb63151631d84f58dedf67958eb9911b9",
    remote = "https://github.com/WebAssembly/testsuite.git",
)

http_archive(
    name = "ic-hs",
    build_file_content = """
package(default_visibility = ["//visibility:public"])
filegroup(name = "artifacts", srcs = glob(["build/*", "test-data/*"]), visibility = ["//visibility:public"])
      """,
    sha256 = "4a70dbe9ff70ac052e101815510135a457869d847f63180442e0afe5c5bd36ef",
    urls = ["https://download.dfinity.systems/ic-ref/ic-ref-test-0.0.1-1431d7b5-x86_64-linux.tar.gz"],
)

# Deployed NNS canisters

http_file(
    name = "mainnet_nns_registry_canister",
    downloaded_file_path = "registry-canister.wasm.gz",
    sha256 = "9bc9448335cb3d459199bd2f8254a79bd6ca30922ede1d07d1ac2cc45c2acff6",
    url = "https://download.dfinity.systems/ic/8b60ab1c8c5a6e4cf29d6413debe190e6cd8a6af/canisters/registry-canister.wasm.gz",
)

http_file(
    name = "mainnet_nns_governance_canister",
    downloaded_file_path = "governance-canister.wasm.gz",
    sha256 = "4dd34e7277e2d53fd653e4a6189597bf9fcfb3383d8fd9113ffdaeb9548cf8da",
    url = "https://download.dfinity.systems/ic/8b60ab1c8c5a6e4cf29d6413debe190e6cd8a6af/canisters/governance-canister.wasm.gz",
)

http_file(
    name = "mainnet_icp_ledger_canister",
    downloaded_file_path = "ledger-canister_notify-method.wasm.gz",
    sha256 = "900e30c4bf3293244cd4649e8470ece822e18723e9a942decabc32d46257b514",
    url = "https://download.dfinity.systems/ic/f02cc38677905e24a9016637fddc697039930808/canisters/ledger-canister_notify-method.wasm.gz",
)

# From https://dashboard.internetcomputer.org/proposal/44955
# Note: the Wasm module hash in the proposal is computed for the uncompressed WASM
http_file(
    name = "mainnet_icp_ledger-archive-node-canister",
    downloaded_file_path = "ledger-archive-node-canister.wasm.gz",
    sha256 = "a88d0b6a327361208a48dd8dab5b7f47f8e1ed0f3c2aca6b6ad383187520ff67",
    url = "https://download.dfinity.systems/ic/1b98a5d984176b1c948d0cb92227d88ad5ee8044/canisters/ledger-archive-node-canister.wasm.gz",
)

http_file(
    name = "mainnet_nns_root-canister",
    downloaded_file_path = "root-canister.wasm.gz",
    sha256 = "a2d3bdcd10ba8c990ad712d9f6fc90d4e7640c7a2c9928e431230002fc89adb2",
    url = "https://download.dfinity.systems/ic/9a1f70d6e1b03ef27e86f8094ee48b83846e482d/canisters/root-canister.wasm.gz",
)

http_file(
    name = "mainnet_nns_lifeline_canister",
    downloaded_file_path = "lifeline-canister.wasm.gz",
    sha256 = "2cce2f281c86325988528624dc9375272ecd2adc96e1021e41d08943a6e9e5a1",
    # TODO: change to https://download.dfinity.systems/ic/9a1f70d6e1b03ef27e86f8094ee48b83846e482d/canisters/lifeline_canister.wasm.gz after the next lifeline canister release.
    url = "https://download.dfinity.systems/ic/9a1f70d6e1b03ef27e86f8094ee48b83846e482d/canisters/lifeline.wasm.gz",
)

http_file(
    name = "mainnet_nns_genesis-token-canister",
    downloaded_file_path = "genesis-token-canister.wasm.gz",
    sha256 = "98cf6a9902e59f6272d03aa281f5247deebf4b61484b54cb80bdd54d6baf6e33",
    url = "https://download.dfinity.systems/ic/9a1f70d6e1b03ef27e86f8094ee48b83846e482d/canisters/genesis-token-canister.wasm.gz",
)

# From https://dashboard.internetcomputer.org/proposal/83624
# Note: the Wasm module hash in the proposal is computed for the uncompressed WASM
http_file(
    name = "mainnet_nns_cycles-minting-canister",
    downloaded_file_path = "cycles-minting-canister.wasm.gz",
    sha256 = "d9b64cbf4fbc05d4f6a7128a5d116a4b3d22706109461bed9385961234443cb8",
    url = "https://download.dfinity.systems/ic/18dc31ca64a4ead42c5d4222e647dce7124eb90e/canisters/cycles-minting-canister.wasm.gz",
)

http_file(
    name = "mainnet_nns_sns-wasm-canister",
    downloaded_file_path = "sns-wasm-canister.wasm.gz",
    sha256 = "727238fdf0081b271f24952a058e9796a68f39d93dbb1269137c16e121cb1549",
    url = "https://download.dfinity.systems/ic/9bb8f35cccd013b38657c67964c39d48fac6353f/canisters/sns-wasm-canister.wasm.gz",
)

# Bitcoin canister

http_file(
    name = "btc_canister",
    downloaded_file_path = "ic-btc-canister.wasm.gz",
    sha256 = "c4153339cc3d23eae8f5e5a9c3872df5e5713913357e11d493bc5d06f257fb47",
    url = "https://github.com/dfinity/bitcoin-canister/releases/download/release%2F2023-03-31/ic-btc-canister.wasm.gz",
)

# Internet Identity canister (test build)

http_file(
    name = "ii_test_canister",
    downloaded_file_path = "internet_identity_test.wasm",
    sha256 = "5f179b2e8bdd637043a223a8ee1be6563ffa699dec071f84c8f8487a9dc8d54c",
    url = "https://github.com/dfinity/internet-identity/releases/download/release-2023-01-16/internet_identity_test.wasm",
)

# SNS aggregator canister (https://github.com/dfinity/nns-dapp/releases/tag/tip)

http_file(
    name = "sns_aggregator",
    downloaded_file_path = "sns_aggregator.wasm",
    sha256 = "428b21acccfa442101096d95f74ab8368724d0e87e14d08f21d9a3bed77b69f3",
    # Keep this dependency while the aggregator release process is WIP (IDX-2661)
    url = "https://github.com/dfinity/nns-dapp/releases/download/ci-pin/sns_aggregator2.wasm",
)

# Import Python rules

http_archive(
    name = "rules_python",
    sha256 = "8c15896f6686beb5c631a4459a3aa8392daccaab805ea899c9d14215074b60ef",
    strip_prefix = "rules_python-0.17.3",
    url = "https://github.com/bazelbuild/rules_python/archive/refs/tags/0.17.3.tar.gz",
)

load("@rules_python//python:repositories.bzl", "python_register_toolchains")

python_register_toolchains(
    name = "python3_10",
    python_version = "3.10",
)

load("@python3_10//:defs.bzl", "interpreter")
load("@rules_python//python:pip.bzl", "pip_parse")

pip_parse(
    name = "python_deps",
    python_interpreter_target = interpreter,
    requirements_lock = "//:requirements.txt",
)

load("@python_deps//:requirements.bzl", "install_deps")

install_deps()
