load("//bazel:defs.bzl", "gzip_compress")
load("//gitlab-ci/src/artifacts:upload.bzl", "upload_artifacts")

CANISTERS = {
    "candid-test-canister.wasm": "//rs/rust_canisters/dfn_candid:candid-test-canister",
    "cycles-minting-canister.wasm": "//rs/nns/cmc:cycles-minting-canister",
    "genesis-token-canister.wasm": "//rs/nns/gtc:genesis-token-canister",
    "governance-canister.wasm": "//rs/nns/governance:governance-canister",
    "governance-canister_test.wasm": "//rs/nns/governance:governance-canister-test",
    "governance-mem-test-canister.wasm": "//rs/nns/integration_tests:governance-mem-test-canister",
    "http_counter.wasm": "//rs/tests/test_canisters/http_counter",
    "ic-ckbtc-minter.wasm": "//rs/bitcoin/ckbtc/minter:ckbtc_minter",
    "ic-icrc1-archive.wasm": "//rs/rosetta-api/icrc1/archive:archive_canister",
    "ic-icrc1-index.wasm": "//rs/rosetta-api/icrc1/index:index_canister",
    "ic-icrc1-ledger.wasm": "//rs/rosetta-api/icrc1/ledger:ledger_canister",
    "ic-nervous-system-common-test-canister.wasm": "//rs/nervous_system/common/test_canister",
    "identity-canister.wasm": "//rs/nns/identity:identity-canister",
    "inter_canister_error_handling.wasm": "//rs/rust_canisters/tests:inter_canister_error_handling",
    "json.wasm": "//rs/rust_canisters/tests:json",
    "ledger-archive-node-canister.wasm": "//rs/rosetta-api/icp_ledger/archive:ledger-archive-node-canister-wasm",
    "ledger-canister.wasm": "//rs/rosetta-api/icp_ledger/ledger:ledger-canister-wasm",
    "ledger-canister_notify-method.wasm": "//rs/rosetta-api/icp_ledger/ledger:ledger-canister-wasm-notify-method",
    "lifeline.wasm": "//rs/nns/handlers/lifeline:lifeline_actor",
    "mem-utils-test-canister.wasm": "//rs/nns/integration_tests:mem-utils-test-canister",
    "memory-test-canister.wasm": "//rs/rust_canisters/memory_test:memory_test_canister",
    "nan_canonicalized.wasm": "//rs/rust_canisters/tests:nan_canonicalized",
    "nns-ui-canister.wasm": "//rs/nns/nns-ui:nns-ui-canister",
    "panics.wasm": "//rs/rust_canisters/tests:panics",
    "pmap_canister.wasm": "//rs/rust_canisters/pmap:pmap_canister",
    "proxy_canister.wasm": "//rs/rust_canisters/proxy_canister",
    "registry-canister.wasm": "//rs/registry/canister:registry-canister",
    "response-payload-test-canister.wasm": "//rs/rust_canisters/response_payload_test:response-payload-test-canister",
    "root-canister.wasm": "//rs/nns/handlers/root:root-canister",
    "sns-governance-canister.wasm": "//rs/sns/governance:sns-governance-canister",
    "sns-governance-canister_test.wasm": "//rs/sns/governance:sns-governance-canister-test",
    "sns-governance-mem-test-canister.wasm": "//rs/sns/integration_tests:sns-governance-mem-test-canister",
    "sns-root-canister.wasm": "//rs/sns/root:sns-root-canister",
    "sns-swap-canister.wasm": "//rs/sns/swap:sns-swap-canister",
    "sns-test-dapp-canister.wasm": "//rs/sns/integration_tests:sns-test-dapp-canister",
    "sns-wasm-canister.wasm": "//rs/nns/sns-wasm:sns-wasm-canister",
    "stable.wasm": "//rs/rust_canisters/tests:stable",
    "statesync-test-canister.wasm": "//rs/rust_canisters/statesync_test:statesync_test_canister",
    "test-notified.wasm": "//rs/rosetta-api/icp_ledger:test_notified_canister",
    "time.wasm": "//rs/rust_canisters/tests:time",
    "upgrade-test-canister.wasm": "//rs/nns/handlers/root:upgrade-test-canister",
    "wasm.wasm": "//rs/rust_canisters/dfn_core:wasm",
    "xnet-test-canister.wasm": "//rs/rust_canisters/xnet_test:xnet-test-canister",
    "cow_safety.wasm": "//rs/tests:src/cow_safety.wasm",
    "counter.wat": "//rs/workload_generator:src/counter.wat",
}

CANISTERS_MAX_SIZE_IN_BYTES = {
    "default": "2097152",
    "cycles-minting-canister.wasm": "2500000",
    "genesis-token-canister.wasm": "2500000",
    "governance-canister.wasm": "2500000",
    "governance-mem-test-canister.wasm": "2500000",
    "ic-ckbtc-minter.wasm": "1900000",
    "ic-icrc1-ledger.wasm": "1900000",
    "ledger-canister.wasm": "1900000",
    "ledger-canisternotify-method.wasm": "1900000",
    "registry-canister.wasm": "3500000",
    "root-canister.wasm": "1500000",
    "sns-canister.wasm": "1500000",
    "sns-governance-canister.wasm": "1700000",
    "sns-governance-canistertest.wasm": "1700000",
    "sns-root-canister.wasm": "1500000",
    "sns-swap-canister.wasm": "1500000",
}

[
    gzip_compress(
        name = name + ".gz",
        srcs = [target],
    )
    for (name, target) in CANISTERS.items()
]

[
    sh_test(
        name = name + "_size_test",
        srcs = ["file_size_test.sh"],
        data = [target],
        env = {
            "FILE": "$(rootpath " + target + ")",
            "MAX_SIZE": CANISTERS_MAX_SIZE_IN_BYTES.get(
                name,
                CANISTERS_MAX_SIZE_IN_BYTES["default"],
            ),
        },
    )
    for (name, target) in CANISTERS.items()
]

filegroup(
    name = "canisters",
    srcs = [name + ".gz" for name in CANISTERS],
    visibility = ["//visibility:public"],
)

upload_artifacts(
    name = "upload",
    inputs = [":canisters"],
    remote_subdir = "canisters",
)
