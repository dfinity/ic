load("@rules_rust//cargo:defs.bzl", "cargo_build_script")
load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")
load("//bazel:defs.bzl", "rust_ic_test")

BUILD_DEPENDENCIES = []

LIB_DEPENDENCIES = [
    # Keep sorted.
    "//packages/ic-ed25519",
    "//packages/ic-error-types",
    "//packages/pocket-ic:pocket-ic",
    "//rs/bitcoin/adapter",
    "//rs/boundary_node/ic_boundary",
    "//rs/canister_sandbox:backend_lib",
    "//rs/config",
    "//rs/crypto/iccsa",
    "//rs/crypto/sha2",
    "//rs/crypto/utils/threshold_sig_der",
    "//rs/http_endpoints/public",
    "//rs/https_outcalls/adapter:adapter_with_http",
    "//rs/https_outcalls/client",
    "//rs/https_outcalls/service",
    "//rs/interfaces",
    "//rs/interfaces/adapter_client",
    "//rs/interfaces/state_manager",
    "//rs/monitoring/logger",
    "//rs/monitoring/metrics",
    "//rs/protobuf",
    "//rs/registry/keys",
    "//rs/registry/proto_data_provider",
    "//rs/registry/routing_table",
    "//rs/registry/subnet_type",
    "//rs/replicated_state",
    "//rs/starter:ic-starter-lib",
    "//rs/state_machine_tests",
    "//rs/state_manager",
    "//rs/test_utilities",
    "//rs/test_utilities/registry",
    "//rs/types/management_canister_types",
    "//rs/types/types",
    "//rs/utils/thread",
    "//rs/validator/ingress_message",
    "@crate_index//:aide",
    "@crate_index//:askama",
    "@crate_index//:axum",
    "@crate_index//:axum-extra",
    "@crate_index//:axum-server",
    "@crate_index//:backoff",
    "@crate_index//:base64",
    "@crate_index//:bitcoin",
    "@crate_index//:bytes",
    "@crate_index//:candid",
    "@crate_index//:clap",
    "@crate_index//:ctrlc",
    "@crate_index//:flate2",
    "@crate_index//:form_urlencoded",
    "@crate_index//:fqdn",
    "@crate_index//:futures",
    "@crate_index//:hex",
    "@crate_index//:http",
    "@crate_index//:http-body-util",
    "@crate_index//:hyper",
    "@crate_index//:hyper-util",
    "@crate_index//:ic-agent",
    "@crate_index//:ic-cdk",
    "@crate_index//:ic-gateway",
    "@crate_index//:ic-utils",
    "@crate_index//:itertools",
    "@crate_index//:libc",
    "@crate_index//:rand",
    "@crate_index//:reqwest",
    "@crate_index//:serde",
    "@crate_index//:serde_cbor",
    "@crate_index//:serde_json",
    "@crate_index//:slog",
    "@crate_index//:strum",
    "@crate_index//:tempfile",
    "@crate_index//:time",
    "@crate_index//:tokio",
    "@crate_index//:tokio-util",
    "@crate_index//:tonic",
    "@crate_index//:tower",
    "@crate_index//:tower-http",
    "@crate_index//:tracing",
    "@crate_index//:tracing-appender",
    "@crate_index//:tracing-subscriber",
    "@crate_index//:uuid",
    "@crate_index//:wat",
]

TEST_DEPENDENCIES = [
    # Keep sorted.
    "//packages/pocket-ic:pocket-ic",
    "//rs/config",
    "//rs/interfaces/registry",
    "//rs/nns/constants",
    "//rs/registry/canister",
    "//rs/registry/proto_data_provider",
    "//rs/registry/routing_table",
    "//rs/registry/transport",
    "//rs/types/management_canister_types",
    "//rs/types/types",
    "@crate_index//:bitcoincore-rpc",
    "@crate_index//:candid",
    "@crate_index//:hex",
    "@crate_index//:ic-agent",
    "@crate_index//:ic-btc-interface",
    "@crate_index//:ic-cdk",
    "@crate_index//:ic-gateway",
    "@crate_index//:ic-utils",
    "@crate_index//:nix",
    "@crate_index//:prometheus",
    "@crate_index//:rcgen",
    "@crate_index//:reqwest",
    "@crate_index//:rustls",
    "@crate_index//:serde",
    "@crate_index//:serde_json",
    "@crate_index//:slog",
    "@crate_index//:tempfile",
    "@crate_index//:tokio",
    "@crate_index//:wat",
]

SPEC_TEST_DEPENDENCIES = TEST_DEPENDENCIES + [
    "//rs/tests/research/spec_compliance",
    "//rs/registry/subnet_type",
]

MACRO_DEPENDENCIES = [
    "@crate_index//:async-trait",
]

ALIASES = {}

cargo_build_script(
    name = "build_script",
    srcs = ["build.rs"],
    aliases = ALIASES,
    data = ["templates/dashboard.html"],  # build script data (e.g. template files) goes here
    deps = BUILD_DEPENDENCIES,
)

rust_binary(
    name = "pocket-ic-server",
    testonly = True,
    srcs = ["src/main.rs"],
    proc_macro_deps = MACRO_DEPENDENCIES,
    # TODO: restrict the visibility
    visibility = ["//visibility:public"],
    deps = LIB_DEPENDENCIES + [
        ":pocket-ic-server-lib",
        "//rs/canister_sandbox:build_script",
    ],
)

rust_library(
    name = "pocket-ic-server-lib",
    testonly = True,
    srcs = [
        "src/lib.rs",
        "src/pocket_ic.rs",
    ] + glob([
        "src/state_api/**/*.rs",
    ]),
    crate_name = "pocket_ic_server",
    proc_macro_deps = MACRO_DEPENDENCIES,
    version = "9.0.3",
    deps = LIB_DEPENDENCIES + [":build_script"],
)

rust_ic_test(
    name = "pic_test",
    srcs = glob(["src/**/*.rs"]),
    proc_macro_deps = MACRO_DEPENDENCIES,
    deps = LIB_DEPENDENCIES + [":build_script"],
)

[
    rust_test(
        name = name,
        size = "medium",
        srcs = [
            "tests/common.rs",
            "tests/" + name + ".rs",
        ],
        aliases = {},
        data = [
            ":pocket-ic-server",
            "//rs/registry/canister:registry-canister",
            "@ii_dev_canister//file",
            "@mozilla_root_ca_store//file",
        ],
        env = {
            "RUST_TEST_THREADS": "2",
            "POCKET_IC_BIN": "$(rootpath :pocket-ic-server)",
            "REGISTRY_WASM": "$(rootpath //rs/registry/canister:registry-canister)",
            "II_WASM": "$(rootpath @ii_dev_canister//file)",
            "SSL_CERT_FILE": "$(rootpath @mozilla_root_ca_store//file)",
        },
        tags = [
            "cpu:8",
            # TODO: remove 'requires-network' tag when the root cause for sporadic error below on Apple Silicon is identified and fixed.
            #  ---- test_http_gateway stdout ----
            #  thread 'test_http_gateway' panicked at rs/pocket_ic_server/tests/test.rs:383:48:
            #  called `Result::unwrap()` on an `Err` value: reqwest::Error {
            #    kind: Request, url: "http://7tjcv-pp777-77776-qaaaa-cai.raw.localhost:49380/",
            #    source: hyper_util::client::legacy::Error(
            #      Connect,
            #      ConnectError("tcp connect error", Os { code: 1, kind: PermissionDenied, message: "Operation not permitted" })
            #    )
            #  }
            "requires-network",
            "test_macos",
        ],
        deps = TEST_DEPENDENCIES,
    )
    for name in [
        "test",
        "gateway",
    ]
]

rust_test(
    name = "spec_test",
    size = "medium",
    srcs = [
        "tests/common.rs",
        "tests/spec_test.rs",
    ],
    data = [
        ":pocket-ic-server",
        "//hs/spec_compliance:ic-ref-test",
        "//rs/tests/httpbin-rs:httpbin",
        "//rs/tests/research:ic-hs",
        "//rs/universal_canister/impl:universal_canister.wasm.gz",
    ],
    env = {
        "HTTPBIN_BIN": "$(rootpath //rs/tests/httpbin-rs:httpbin)",
        "POCKET_IC_BIN": "$(rootpath :pocket-ic-server)",
        "IC_REF_TEST_ROOT": "rs/tests/research/ic-hs",
        "IC_REF_TEST_BIN": "$(rootpath //hs/spec_compliance:ic-ref-test)",
        "UNIVERSAL_CANISTER_WASM_PATH": "$(rootpath //rs/universal_canister/impl:universal_canister.wasm.gz)",
    },
    tags = [
        "cpu:8",
    ],
    deps = SPEC_TEST_DEPENDENCIES,
)

rust_test(
    name = "bitcoin_integration_tests",
    size = "small",
    srcs = [
        "tests/bitcoin_integration_tests.rs",
    ],
    aliases = {},
    data = [
        ":pocket-ic-server",
        "//:bitcoind",
        "@bitcoin_example_canister//file",
        "@btc_canister//file",
        "@mozilla_root_ca_store//file",
    ],
    env = {
        "POCKET_IC_BIN": "$(rootpath :pocket-ic-server)",
        "BASIC_BITCOIN_WASM": "$(rootpath @bitcoin_example_canister//file)",
        "BITCOIND_BIN": "$(rootpath //:bitcoind)",
        "BTC_WASM": "$(rootpath @btc_canister//file)",
        "SSL_CERT_FILE": "$(rootpath @mozilla_root_ca_store//file)",
    },
    tags = [
        # TODO: remove 'requires-network' tag when the root cause for sporadic error below on Apple Silicon is identified and fixed.
        # Failed to crate http gateway: Failed to bind to address 127.0.0.1:0: Operation not permitted (os error 1)
        "requires-network",
        "test_macos",
    ],
    deps = TEST_DEPENDENCIES,
)
