load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_doc", "rust_doc_test", "rust_library", "rust_test")
load("//bazel:canbench.bzl", "rust_canbench")
load("//bazel:canisters.bzl", "rust_canister")
load("//bazel:defs.bzl", "rust_ic_test_suite")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "helper_contracts",
    srcs = [
        "DepositHelperWithSubaccount.sol",
        "ERC20DepositHelper.sol",
        "EthDepositHelper.sol",
    ],
    visibility = ["//visibility:public"],
)

LIB_DEPS = [
    # Keep sorted.
    "//packages/ic-ethereum-types",
    "//packages/ic-secp256k1",
    "//packages/icrc-cbor:icrc-cbor_u256",
    "//packages/icrc-ledger-client-cdk:icrc_ledger_client_cdk",
    "//packages/icrc-ledger-types:icrc_ledger_types",
    "//rs/phantom_newtype",
    "//rs/types/management_canister_types",
    "//rs/utils/ensure",
    "@crate_index//:candid",
    "@crate_index//:ethnum",
    "@crate_index//:evm_rpc_client",
    "@crate_index//:evm_rpc_types",
    "@crate_index//:futures",
    "@crate_index//:hex",
    "@crate_index//:hex-literal",
    "@crate_index//:ic-canister-log",
    "@crate_index//:ic-canister-runtime",
    "@crate_index//:ic-cdk",
    "@crate_index//:ic-cdk-timers",
    "@crate_index//:ic-metrics-encoder",
    "@crate_index//:ic-sha3",
    "@crate_index//:ic-stable-structures",
    "@crate_index//:minicbor",
    "@crate_index//:num-bigint",
    "@crate_index//:num-traits",
    "@crate_index//:rlp",
    "@crate_index//:scopeguard",
    "@crate_index//:serde",
    "@crate_index//:serde_bytes",
    "@crate_index//:serde_json",
    "@crate_index//:strum",
    "@crate_index//:thiserror",
    "@crate_index//:thousands",
]

[
    rust_library(
        name = name,
        srcs = glob(
            ["src/**/*.rs"],
            exclude = [
                "src/main.rs",
                "src/dashboard.rs",
                "src/dashboard/tests.rs",
            ],
        ),
        crate_features = features,
        crate_name = "ic_cketh_minter",
        proc_macro_deps = [
            # Keep sorted.
            "@crate_index//:strum_macros",
        ],
        version = "0.1.0",
        deps = LIB_DEPS + extra_deps,
    )
    for (name, features, extra_deps) in [
        (
            "minter",
            [],
            [],
        ),
        (
            "minter-canbench",
            ["canbench-rs"],
            ["@crate_index//:canbench-rs"],
        ),
    ]
]

rust_doc(
    name = "doc",
    crate = ":minter",
)

rust_doc_test(
    name = "doc_test",
    crate = ":minter",
)

rust_test(
    name = "lib_tests",
    crate = ":minter",
    data = [
        "test_resources/mainnet_events.gz",
        "test_resources/sepolia_events.gz",
    ],
    env = {
        "CARGO_MANIFEST_DIR": "rs/ethereum/cketh/minter",
    },
    deps = [
        # Keep sorted.
        ":minter",
        "//rs/crypto/test_utils/reproducible_rng",
        "//rs/ethereum/cketh/test_utils",
        "@crate_index//:assert_matches",
        "@crate_index//:candid_parser",
        "@crate_index//:ethers-core",
        "@crate_index//:flate2",
        "@crate_index//:ic-agent",
        "@crate_index//:maplit",
        "@crate_index//:proptest",
        "@crate_index//:rand",
        "@crate_index//:tempfile",
        "@crate_index//:tokio",
    ],
)

rust_test(
    name = "unit_tests",
    crate = ":_wasm_cketh_minter",
    data = [":cketh_minter.did"],
    env = {
        "CARGO_MANIFEST_DIR": "rs/ethereum/cketh/minter",
    },
    deps = [
        # Keep sorted.
        ":minter",
        "@crate_index//:candid_parser",
        "@crate_index//:maplit",
        "@crate_index//:scraper",
    ],
)

[rust_canister(
    name = "cketh_minter" + target_suffix,
    srcs = glob(["src/**/*.rs"]),
    compile_data = [
        "templates/dashboard.html",
        "templates/pagination.html",
        "templates/principal_to_bytes.js",
    ],
    crate_features = features,
    crate_name = "ic_cketh_minter_canister",
    proc_macro_deps = [
        # Keep sorted.
    ],
    service_file = "cketh_minter.did",
    deps = [
        # Keep sorted.
        ":minter",
        "//packages/ic-ethereum-types",
        "//packages/ic-http-types",
        "//packages/ic-secp256k1",
        "//packages/icrc-ledger-client-cdk:icrc_ledger_client_cdk",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "@crate_index//:askama",
        "@crate_index//:candid",
        "@crate_index//:futures",
        "@crate_index//:hex",
        "@crate_index//:ic-canister-log",
        "@crate_index//:ic-cdk",
        "@crate_index//:ic-cdk-timers",
        "@crate_index//:ic-metrics-encoder",
        "@crate_index//:minicbor",
        "@crate_index//:num-traits",
        "@crate_index//:serde_bytes",
        "@crate_index//:time",
    ],
) for (target_suffix, features) in [
    ("", []),
    (
        "_debug",
        ["debug_checks"],
    ),
]]

rust_binary(
    name = "principal_to_hex",
    srcs = ["bin/principal_to_hex.rs"],
    deps = [
        # Keep sorted.
        "@crate_index//:candid",
        "@crate_index//:hex",
    ],
)

rust_ic_test_suite(
    name = "integration_tests",
    # the test sometimes times out on CI with default timeout
    # of "moderate" (5 minutes) - 2025-07-11
    timeout = "long",
    srcs = glob(
        ["tests/**/*.rs"],
        exclude = ["tests/dump_stable_memory.rs"],
    ),
    data = [
        ":cketh_minter_debug.wasm.gz",
        "//rs/ethereum/ledger-suite-orchestrator:ledger_suite_orchestrator_canister.wasm.gz",
        "//rs/ledger_suite/icrc1/archive:archive_canister_u256.wasm.gz",
        "//rs/ledger_suite/icrc1/index-ng:index_ng_canister_u256.wasm.gz",
        "//rs/ledger_suite/icrc1/ledger:ledger_canister_u256.wasm.gz",
        "@evm_rpc.wasm.gz//file",
    ],
    env = {
        "CARGO_MANIFEST_DIR": "rs/ethereum/cketh/minter",
        "CKETH_MINTER_WASM_PATH": "$(rootpath :cketh_minter_debug.wasm.gz)",
        "LEDGER_SUITE_ORCHESTRATOR_WASM_PATH": "$(rootpath //rs/ethereum/ledger-suite-orchestrator:ledger_suite_orchestrator_canister.wasm.gz)",
        "LEDGER_CANISTER_WASM_PATH": "$(rootpath //rs/ledger_suite/icrc1/ledger:ledger_canister_u256.wasm.gz)",
        "INDEX_CANISTER_WASM_PATH": "$(rootpath //rs/ledger_suite/icrc1/index-ng:index_ng_canister_u256.wasm.gz)",
        "LEDGER_ARCHIVE_NODE_CANISTER_WASM_PATH": "$(rootpath //rs/ledger_suite/icrc1/archive:archive_canister_u256.wasm.gz)",
        "EVM_RPC_CANISTER_WASM_PATH": "$(rootpath @evm_rpc.wasm.gz//file)",
    },
    proc_macro_deps = [],
    deps = [
        # Keep sorted.
        ":minter",
        "//packages/ic-ethereum-types",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "//rs/ethereum/cketh/test_utils",
        "//rs/ethereum/ledger-suite-orchestrator/test_utils",
        "//rs/state_machine_tests",
        "//rs/types/base_types",
        "//rs/types/management_canister_types",
        "@crate_index//:assert_matches",
        "@crate_index//:candid",
        "@crate_index//:ethers-core",
        "@crate_index//:hex",
        "@crate_index//:minicbor",
        "@crate_index//:num-bigint",
        "@crate_index//:num-traits",
        "@crate_index//:serde",
        "@crate_index//:serde_bytes",
        "@crate_index//:serde_json",
    ],
)

rust_binary(
    name = "cketh_minter_dump_stable_memory",
    testonly = True,
    srcs = ["tests/dump_stable_memory.rs"],
    deps = [
        # Keep sorted.
        ":minter",
        "//packages/ic-ethereum-types",
        "//rs/phantom_newtype",
        "@crate_index//:candid",
        "@crate_index//:ethers-core",
        "@crate_index//:ethnum",
        "@crate_index//:flate2",
        "@crate_index//:ic-stable-structures",
        "@crate_index//:num-traits",
        "@crate_index//:rlp",
    ],
)

genrule(
    name = "mainnet_events.mem.gz",
    testonly = True,
    srcs = [
        ":cketh_minter_dump_stable_memory",
        "test_resources/mainnet_events.gz",
    ],
    outs = ["test_resources/mainnet_events.mem.gz"],
    cmd_bash = """
    export CARGO_MANIFEST_DIR=rs/ethereum/cketh/minter
    $(location :cketh_minter_dump_stable_memory) $(location test_resources/mainnet_events.gz) $@
    """,
    visibility = ["//visibility:private"],
)

rust_canbench(
    name = "cketh_minter_canbench",
    srcs = glob(["src/**/*.rs"]),
    compile_data = [
        "templates/dashboard.html",
        "templates/pagination.html",
        "templates/principal_to_bytes.js",
        ":cketh_minter.did",
    ],
    data = ["mainnet_events.mem.gz"],
    env = {
        "CANBENCH_STABLE_MEMORY_FILE": "$(location mainnet_events.mem.gz)",
        # The values were take from proposal [126314](https://dashboard.internetcomputer.org/proposal/126314)
        #  and the hex arg was produced by the command:
        #
        #   didc encode -d ./cketh_minter.did -t '(MinterArg)' '(
        #     variant {
        #       InitArg = record {
        #         ethereum_network = variant { Mainnet };
        #         ecdsa_key_name = "key_1";
        #         ethereum_contract_address = opt "0x7574eB42cA208A4f6960ECCAfDF186D627dCC175";
        #         ledger_id = principal "ss2fx-dyaaa-aaaar-qacoq-cai";
        #         ethereum_block_height = variant { Finalized };
        #         minimum_withdrawal_amount = 30_000_000_000_000_000 : nat;
        #         next_transaction_nonce = 0 : nat;
        #         last_scraped_block_number = 18_676_637 : nat;
        #         evm_rpc_id = null;
        #       }
        #     }
        #   )'
        "CANBENCH_INIT_ARGS_HEX": "4449444c096b02ba9edbce040186dac7c609076c0adeb3d8340282a6fca50303f88c88a70304d380e9ae03048dfb97ee0402fe8d8ea90503cf8995cf0a02b1f3c3e50b03cda8bfe40d03e1d887b30e056e716e7d6e686e066b03edd29ab9037fe689d6fc037fe7f2ddcf087f6c09a6fa9ac50108f3eabee3017db6a9f6dc027182a6fca5037df88c88a70304f1f7fcf70668cf8995cf0a02b1f3c3e50b7de1d887b30e066b02a4d3bb457fb3d1cba00a7f010001009df7f308056b65795f310000010a000000000230009d0101012a30783735373465423432634132303841346636393630454343416644463138364436323764434331373580808cfaf49aa53501",
    },
    results_file = "canbench/results.yml",
    rustc_env = {
        "CKETH_MINTER_DID_PATH": "$(execpath :cketh_minter.did)",
    },
    visibility = ["//visibility:private"],
    deps = [
        # Keep sorted.
        ":minter-canbench",
        "//packages/ic-ethereum-types",
        "//packages/ic-http-types",
        "//packages/ic-secp256k1",
        "//packages/icrc-ledger-client-cdk:icrc_ledger_client_cdk",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "@crate_index//:askama",
        "@crate_index//:candid",
        "@crate_index//:futures",
        "@crate_index//:hex",
        "@crate_index//:ic-canister-log",
        "@crate_index//:ic-cdk",
        "@crate_index//:ic-cdk-timers",
        "@crate_index//:ic-metrics-encoder",
        "@crate_index//:minicbor",
        "@crate_index//:num-traits",
        "@crate_index//:serde_bytes",
        "@crate_index//:time",
    ],
)
