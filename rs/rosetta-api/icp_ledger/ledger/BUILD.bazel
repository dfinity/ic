load("@rules_rust//rust:defs.bzl", "rust_library", "rust_test")
load("//bazel:canisters.bzl", "rust_canister")
load("//bazel:defs.bzl", "rust_ic_test")

package(default_visibility = ["//visibility:public"])

rust_library(
    name = "ledger",
    srcs = [
        "src/dfn_runtime.rs",
        "src/lib.rs",
        "src/tests.rs",
    ],
    compile_data = [
        "//rs/rosetta-api/icp_ledger/archive:ledger-archive-node-canister-wasm.wasm.gz",
    ],
    crate_name = "ledger_canister",
    proc_macro_deps = [
        # Keep sorted.
        "@crate_index//:async-trait",
    ],
    rustc_env = {
        "LEDGER_ARCHIVE_NODE_CANISTER_WASM_PATH": "$(execpath //rs/rosetta-api/icp_ledger/archive:ledger-archive-node-canister-wasm.wasm.gz)",
    },
    version = "0.9.0",
    deps = [
        # Keep sorted.
        "//packages/ic-ledger-hash-of:ic_ledger_hash_of",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "//rs/crypto/sha2",
        "//rs/limits",
        "//rs/rosetta-api/icp_ledger",
        "//rs/rosetta-api/icrc1",
        "//rs/rosetta-api/ledger_canister_core",
        "//rs/rosetta-api/ledger_core",
        "//rs/rust_canisters/dfn_candid",
        "//rs/rust_canisters/dfn_core",
        "//rs/types/base_types",
        "@crate_index//:candid",
        "@crate_index//:intmap",
        "@crate_index//:lazy_static",
        "@crate_index//:num-traits",
        "@crate_index//:serde",
        "@crate_index//:serde_bytes",
        "@crate_index//:serde_cbor",
    ],
)

rust_test(
    name = "ledger_test",
    compile_data = [
        "//rs/rosetta-api/icp_ledger/archive:ledger-archive-node-canister-wasm.wasm.gz",
    ],
    crate = ":ledger",
    data = [
        "//rs/rosetta-api/icp_ledger/archive:ledger-archive-node-canister-wasm.wasm.gz",
    ],
    rustc_env = {
        "LEDGER_ARCHIVE_NODE_CANISTER_WASM_PATH": "$(execpath //rs/rosetta-api/icp_ledger/archive:ledger-archive-node-canister-wasm.wasm.gz)",
    },
)

LEDGER_CANISTER_DEPS = [
    # Keep sorted.
    ":ledger",
    "//packages/ic-ledger-hash-of:ic_ledger_hash_of",
    "//packages/icrc-ledger-types:icrc_ledger_types",
    "//rs/rosetta-api/icp_ledger:icp_ledger",
    "//rs/rosetta-api/icrc1",
    "//rs/rosetta-api/ledger_canister_core",
    "//rs/rosetta-api/ledger_core",
    "//rs/rust_canisters/canister_log",
    "//rs/rust_canisters/dfn_candid",
    "//rs/rust_canisters/dfn_core",
    "//rs/rust_canisters/dfn_http_metrics",
    "//rs/rust_canisters/dfn_protobuf",
    "//rs/rust_canisters/on_wire",
    "//rs/types/base_types",
    "@crate_index//:candid",
    "@crate_index//:ciborium",
    "@crate_index//:ic-metrics-encoder",
    "@crate_index//:num-traits",
    "@crate_index//:serde_bytes",
]

LEDGER_CANISTER_DATA = [
    "//rs/rosetta-api/icp_ledger:ledger.did",
    "//rs/rosetta-api/icp_ledger/ledger:ledger_candid_backwards_compatible.did",
]

LEDGER_CANISTER_RUSTC_ENV = {
    "LEDGER_DID_PATH": "$(execpath //rs/rosetta-api/icp_ledger:ledger.did)",
}

rust_canister(
    name = "ledger-canister-wasm",
    srcs = ["src/main.rs"],
    compile_data = LEDGER_CANISTER_DATA,
    data = LEDGER_CANISTER_DATA,
    rustc_env = LEDGER_CANISTER_RUSTC_ENV,
    service_file = "//rs/rosetta-api/icp_ledger:ledger.did",
    deps = LEDGER_CANISTER_DEPS,
)

rust_canister(
    name = "ledger-canister-wasm-notify-method",
    srcs = ["src/main.rs"],
    compile_data = LEDGER_CANISTER_DATA,
    crate_features = ["notify-method"],
    data = LEDGER_CANISTER_DATA,
    rustc_env = LEDGER_CANISTER_RUSTC_ENV,
    service_file = "//rs/rosetta-api/icp_ledger:ledger.did",
    deps = LEDGER_CANISTER_DEPS,
)

rust_test(
    name = "ledger_canister_unit_test",
    compile_data = LEDGER_CANISTER_DATA,
    crate = ":_wasm_ledger-canister-wasm",
    data = LEDGER_CANISTER_DATA + ["//rs/rosetta-api/icp_ledger:ledger_archive.did"],
    env = {
        "CARGO_MANIFEST_DIR": "rs/rosetta-api/icp_ledger/ledger",
    },
    rustc_env = LEDGER_CANISTER_RUSTC_ENV,
    deps = ["@crate_index//:candid_parser"],
)

rust_ic_test(
    name = "ledger_canister_test",
    srcs = ["tests/tests.rs"],
    data = [
        ":ledger-canister-wasm",
        "@mainnet_icp_ledger_canister//file",
    ],
    env = {
        "CARGO_MANIFEST_DIR": "rs/rosetta-api/icp_ledger/ledger",
        "ICP_LEDGER_DEPLOYED_VERSION_WASM_PATH": "$(rootpath @mainnet_icp_ledger_canister//file)",
        "LEDGER_CANISTER_WASM_PATH": "$(rootpath :ledger-canister-wasm)",
    },
    deps = [
        # Keep sorted.
        ":ledger",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "//rs/rosetta-api/icp_ledger",
        "//rs/rosetta-api/icrc1",
        "//rs/rosetta-api/icrc1/ledger/sm-tests",
        "//rs/rosetta-api/icrc1/test_utils",
        "//rs/rosetta-api/ledger_core",
        "//rs/rust_canisters/dfn_candid",
        "//rs/rust_canisters/dfn_protobuf",
        "//rs/rust_canisters/on_wire",
        "//rs/state_machine_tests",
        "//rs/test_utilities/load_wasm",
        "//rs/types/base_types",
        "@crate_index//:candid",
        "@crate_index//:hex",
        "@crate_index//:ic-agent",
        "@crate_index//:num-traits",
        "@crate_index//:serde_bytes",
    ],
)

# Deploys the ledger canister to a running local replica.
sh_binary(
    name = "dfx_deploy",
    srcs = ["scripts/dfx_deploy.sh"],
    data = [
        "dfx.json",
        ":ledger-canister-wasm",
        "//rs/rosetta-api/icp_ledger:ledger.did",
    ],
    visibility = ["//visibility:public"],
)
