load("@rules_rust//rust:defs.bzl", "rust_library", "rust_test")
load("//bazel:canisters.bzl", "rust_canister")
load("//bazel:defs.bzl", "rust_ic_test_suite_with_extra_srcs")

package(default_visibility = ["//visibility:public"])

DEPENDENCIES = [
    # Keep sorted.
    "//packages/icrc-ledger-types:icrc_ledger_types",
    "//rs/crypto/sha2",
    "//rs/rosetta-api/icrc1",
    "//rs/rosetta-api/ledger_canister_core",
    "//rs/rosetta-api/ledger_core",
    "//rs/rust_canisters/canister_log",
    "//rs/rust_canisters/canister_profiler",
    "//rs/rust_canisters/http_types",
    "//rs/types/base_types",
    "@crate_index//:candid",
    "@crate_index//:ciborium",
    "@crate_index//:ic-cdk",
    "@crate_index//:ic-cdk-timers",
    "@crate_index//:ic-metrics-encoder",
    "@crate_index//:ic-stable-structures",
    "@crate_index//:num-traits",
    "@crate_index//:scopeguard",
    "@crate_index//:serde",
    "@crate_index//:serde_bytes",
    "@crate_index//:serde_json",
]

MACRO_DEPENDENCIES = [
    # Keep sorted.
    "@crate_index//:ic-cdk-macros",
]

rust_library(
    name = "index-ng",
    srcs = ["src/lib.rs"],
    crate_name = "ic_icrc1_index_ng",
    edition = "2018",
    proc_macro_deps = MACRO_DEPENDENCIES,
    deps = DEPENDENCIES,
)

[
    rust_canister(
        name = "index_ng_canister" + name_suffix,
        srcs = [
            "src/logs.rs",
            "src/main.rs",
        ],
        compile_data = [":index-ng.did"],
        crate_features = features,
        crate_name = "ic_icrc1_index_ng_canister",
        edition = "2018",
        opt = "z",
        proc_macro_deps = MACRO_DEPENDENCIES,
        rustc_env = {
            "INDEX_DID_PATH": "$(location :index-ng.did)",
        },
        service_file = ":index-ng.did",
        deps = [
            # Keep sorted.
            ":index-ng",
        ] + DEPENDENCIES + extra_deps,
    )
    for (name_suffix, features, extra_deps) in [
        (
            "",
            [],
            ["//rs/rosetta-api/icrc1/tokens_u64"],
        ),
        (
            "_u256",
            ["u256-tokens"],
            ["//rs/rosetta-api/icrc1/tokens_u256"],
        ),
    ]
]

rust_test(
    name = "index_ng_unit_test",
    crate = ":_wasm_index_ng_canister",
    data = [":index-ng.did"],
    deps = [
        # Keep sorted.
        "//rs/rosetta-api/icrc1/test_utils",
        "@crate_index//:candid_parser",
        "@crate_index//:proptest",
    ],
)

[
    rust_ic_test_suite_with_extra_srcs(
        name = "index_ng_test" + conf["test_suffix"],
        timeout = "long",
        srcs = [
            "tests/retrieve_blocks_from_ledger_interval.rs",
            "tests/tests.rs",
        ],
        crate_features = conf["crate_features"],
        data = [
            "//rs/rosetta-api/icrc1/index:index_canister.wasm",
        ] + [
            conf["index_wasm"],
            conf["ledger_wasm"],
        ],
        env = {
            "CARGO_MANIFEST_DIR": "rs/rosetta-api/icrc1/index-ng",
            "IC_ICRC1_INDEX_NG_WASM_PATH": "$(rootpath " + conf["index_wasm"] + ")",
            "IC_ICRC1_INDEX_WASM_PATH": "$(rootpath //rs/rosetta-api/icrc1/index:index_canister.wasm)",
            "IC_ICRC1_LEDGER_WASM_PATH": "$(rootpath " + conf["ledger_wasm"] + ")",
        },
        extra_srcs = ["tests/common/mod.rs"],
        deps = [
            # Keep sorted.
            ":index-ng",
            "//packages/icrc-ledger-types:icrc_ledger_types",
            "//rs/registry/subnet_type",
            "//rs/rosetta-api/icrc1",
            "//rs/rosetta-api/icrc1/index",
            "//rs/rosetta-api/icrc1/ledger",
            "//rs/rosetta-api/icrc1/ledger/sm-tests",
            "//rs/rosetta-api/icrc1/test_utils",
            "//rs/rosetta-api/icrc1/tokens_u64",
            "//rs/rosetta-api/ledger_canister_core",
            "//rs/rosetta-api/ledger_core",
            "//rs/rosetta-api/test_utils",
            "//rs/rust_canisters/http_types",
            "//rs/state_machine_tests",
            "//rs/test_utilities/load_wasm",
            "//rs/types/base_types",
            "@crate_index//:candid",
            "@crate_index//:ic-agent",
            "@crate_index//:num-traits",
            "@crate_index//:proptest",
            "@crate_index//:serde",
            "@crate_index//:serde_bytes",
            "@crate_index//:serde_json",
        ],
    )
    for conf in [
        # The Index must be backward compatible with the version of the Ledger
        # that doesn't support ICRC-3 yet. Therefore we run a set of tests for
        # the Ledger in the current commit, which supports ICRC-3, as well as
        # a set of tests for the Ledger without ICRC-3 downloaded from a
        # previous commit.
        # - Ledger with ICRC-3 and with get-blocks
        {
            "crate_features": [],
            "index_wasm": ":index_ng_canister.wasm",
            "ledger_wasm": "//rs/rosetta-api/icrc1/ledger:ledger_canister.wasm",
            "test_suffix": "",
        },
        {
            "crate_features": [],
            "index_wasm": ":index_ng_canister_u256.wasm",
            "ledger_wasm": "//rs/rosetta-api/icrc1/ledger:ledger_canister_u256.wasm",
            "test_suffix": "_u256",
        },
        # - Ledger with ICRC-3 and without get-blocks
        {
            "crate_features": ["get_blocks_disabled"],
            "index_wasm": ":index_ng_canister.wasm",
            "ledger_wasm": "//rs/rosetta-api/icrc1/ledger:ledger_canister_getblocksdisabled.wasm",
            "test_suffix": "_wo_getblocks",
        },
        {
            "crate_features": ["get_blocks_disabled"],
            "index_wasm": ":index_ng_canister_u256.wasm",
            "ledger_wasm": "//rs/rosetta-api/icrc1/ledger:ledger_canister_u256_getblocksdisabled.wasm",
            "test_suffix": "_u256_wo_getblocks",
        },
        # - Ledger without ICRC-3 and with get-blocks
        {
            "crate_features": ["icrc3_disabled"],
            "index_wasm": ":index_ng_canister.wasm",
            "ledger_wasm": "@ic-icrc1-ledger-wo-icrc-3.wasm.gz//file",
            "test_suffix": "_wo_icrc3",
        },
        {
            "crate_features": ["icrc3_disabled"],
            "index_wasm": ":index_ng_canister_u256.wasm",
            "ledger_wasm": "@ic-icrc1-ledger-wo-icrc-3-u256.wasm.gz//file",
            "test_suffix": "_wo_icrc3_u256",
        },
    ]
]
