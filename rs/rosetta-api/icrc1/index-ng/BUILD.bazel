load("@rules_rust//rust:defs.bzl", "rust_library", "rust_test")
load("//bazel:canisters.bzl", "rust_canister")
load("//bazel:defs.bzl", "gzip_compress")

package(default_visibility = ["//visibility:public"])

DEPENDENCIES = [
    "//rs/crypto/sha2",
    "//packages/icrc-ledger-types:icrc_ledger_types",
    "//rs/rosetta-api/icrc1",
    "//rs/rosetta-api/ledger_core",
    "//rs/rust_canisters/canister_profiler",
    "//rs/rust_canisters/http_types",
    "//rs/rust_canisters/canister_log",
    "//rs/types/base_types",
    "@crate_index//:candid",
    "@crate_index//:ciborium",
    "@crate_index//:ic-cdk",
    "@crate_index//:ic-cdk-timers",
    "@crate_index//:ic-metrics-encoder",
    "@crate_index//:num-traits",
    "@crate_index//:scopeguard",
    "@crate_index//:serde",
    "@crate_index//:serde_bytes",
    "@crate_index//:serde_json",
    "@crate_index//:ic-stable-structures",
]

MACRO_DEPENDENCIES = [
    "@crate_index//:ic-cdk-macros",
]

rust_library(
    name = "index-ng",
    srcs = ["src/lib.rs"],
    crate_name = "ic_icrc1_index_ng",
    edition = "2018",
    proc_macro_deps = MACRO_DEPENDENCIES,
    deps = DEPENDENCIES,
)

[
    rust_canister(
        name = "index_ng_canister" + name_suffix,
        srcs = [
            "src/logs.rs",
            "src/main.rs",
        ],
        compile_data = [":index-ng.did"],
        crate_features = features,
        crate_name = "ic_icrc1_index_ng_canister",
        edition = "2018",
        proc_macro_deps = MACRO_DEPENDENCIES,
        rustc_env = {
            "INDEX_DID_PATH": "$(location :index-ng.did)",
        },
        service_file = ":index-ng.did",
        deps = [
            ":index-ng",
        ] + DEPENDENCIES + extra_deps,
    )
    for (name_suffix, features, extra_deps) in [
        (
            "",
            [],
            ["//rs/rosetta-api/icrc1/tokens_u64"],
        ),
        (
            "_u256",
            ["u256-tokens"],
            ["//rs/rosetta-api/icrc1/tokens_u256"],
        ),
    ]
]

[
    gzip_compress(
        name = "index_ng_canister" + name_suffix + ".wasm.gz",
        srcs = [":index_ng_canister" + name_suffix],
    )
    for name_suffix in [
        "",
        "_u256",
    ]
]

rust_test(
    name = "index_ng_unit_test",
    crate = ":_wasm_index_ng_canister",
    data = [":index-ng.did"],
    deps = [
        "//rs/rosetta-api/icrc1/test_utils",
        "@crate_index//:candid_parser",
        "@crate_index//:proptest",
    ],
)

[
    rust_test(
        name = "index_ng_test" + test_suffix,
        srcs = ["tests/tests.rs"],
        data = [
            ":index_ng_canister" + name_suffix + ".wasm",
            "//rs/rosetta-api/icrc1/index:index_canister.wasm",
            "//rs/rosetta-api/icrc1/ledger:ledger_canister" + name_suffix + ".wasm",
        ] + extra_data,
        env = {
            "CARGO_MANIFEST_DIR": "rs/rosetta-api/icrc1/index-ng",
            "IC_ICRC1_INDEX_WASM_PATH": "$(rootpath //rs/rosetta-api/icrc1/index:index_canister.wasm)",
            "IC_ICRC1_INDEX_NG_WASM_PATH": "$(rootpath :index_ng_canister" + name_suffix + ".wasm)",
            "IC_ICRC1_LEDGER_WASM_PATH": ledger_wasm,
        },
        deps = [
            ":index-ng",
            "//packages/icrc-ledger-types:icrc_ledger_types",
            "//rs/rosetta-api/icrc1",
            "//rs/rosetta-api/icrc1/index",
            "//rs/rosetta-api/icrc1/ledger",
            "//rs/rosetta-api/icrc1/test_utils",
            "//rs/rosetta-api/icrc1/tokens_u64",
            "//rs/rosetta-api/ledger_canister_core",
            "//rs/rosetta-api/ledger_core",
            "//rs/rust_canisters/http_types",
            "//rs/state_machine_tests",
            "//rs/test_utilities/load_wasm",
            "//rs/types/base_types",
            "@crate_index//:assert_matches",
            "@crate_index//:candid",
            "@crate_index//:ic-agent",
            "@crate_index//:num-traits",
            "@crate_index//:proptest",
            "@crate_index//:serde",
            "@crate_index//:serde_bytes",
            "@crate_index//:serde_json",
        ],
    )
    for test_suffix, name_suffix, extra_data, ledger_wasm in [
        # The Index must be backward compatible with the version of the Ledger
        # that doesn't support ICRC-3 yet. Therefore we run a set of tests for
        # the Ledger in the current commit, which supports ICRC-3, as well as
        # a set of tests for the Ledger without ICRC-3 downloaded from a
        # previous commit.
        # - Ledger with ICRC-3
        ("", "", [], "$(rootpath //rs/rosetta-api/icrc1/ledger:ledger_canister.wasm)"),
        ("_u256", "_u256", [], "$(rootpath //rs/rosetta-api/icrc1/ledger:ledger_canister_u256.wasm)"),
        # - Ledger without ICRC-3
        (
            "_wo_icrc3",
            "",
            ["@ic-icrc1-ledger-wo-icrc-3.wasm.gz//file"],
            "$(rootpath @ic-icrc1-ledger-wo-icrc-3.wasm.gz//file)",
        ),
        (
            "_wo_icrc3_u256",
            "_u256",
            ["@ic-icrc1-ledger-wo-icrc-3-u256.wasm.gz//file"],
            "$(rootpath @ic-icrc1-ledger-wo-icrc-3-u256.wasm.gz//file)",
        ),
    ]
]
