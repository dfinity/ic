load("@rules_rust//rust:defs.bzl", "rust_library", "rust_test")
load("//bazel:canisters.bzl", "rust_canister")
load("//bazel:defs.bzl", "rust_ic_test")

package(default_visibility = ["//visibility:public"])

[
    rust_library(
        name = "ledger" + name_suffix,
        srcs = glob(["src/**/*.rs"]),
        compile_data = [
            "//rs/rosetta-api/icrc1/archive:archive_canister" + name_suffix,
        ],
        crate_name = "ic_icrc1_ledger",
        proc_macro_deps = [
            "@crate_index//:async-trait",
        ],
        rustc_env = {
            "IC_ICRC1_ARCHIVE_WASM_PATH": "$(execpath //rs/rosetta-api/icrc1/archive:archive_canister" + name_suffix + ")",
        },
        version = "0.9.0",
        deps = [
            "//packages/ic-ledger-hash-of:ic_ledger_hash_of",
            "//packages/icrc-ledger-types:icrc_ledger_types",
            "//rs/crypto/tree_hash",
            "//rs/rosetta-api/icrc1",
            "//rs/rosetta-api/ledger_canister_core",
            "//rs/rosetta-api/ledger_core",
            "//rs/rust_canisters/canister_log",
            "//rs/types/base_types",
            "@crate_index//:candid",
            "@crate_index//:ciborium",
            "@crate_index//:hex",
            "@crate_index//:ic-cdk",
            "@crate_index//:ic-metrics-encoder",
            "@crate_index//:serde",
            "@crate_index//:serde_bytes",
        ],
    )
    for name_suffix in [
        "",
        "_u256",
    ]
]

[
    rust_canister(
        name = "ledger_canister" + name_suffix,
        srcs = ["src/main.rs"],
        crate_features = features,
        crate_name = "ic_icrc1_ledger_canister" + name_suffix,
        opt = "z",
        proc_macro_deps = [
            "@crate_index//:ic-cdk-macros",
        ],
        service_file = ":ledger.did",
        deps = [
            "//packages/icrc-ledger-types:icrc_ledger_types",
            "//rs/rosetta-api/icrc1",
            "//rs/rosetta-api/ledger_canister_core",
            "//rs/rosetta-api/ledger_core",
            "//rs/rust_canisters/canister_log",
            "//rs/rust_canisters/http_types",
            "//rs/types/base_types",
            "@crate_index//:candid",
            "@crate_index//:ciborium",
            "@crate_index//:ic-cdk",
            "@crate_index//:ic-metrics-encoder",
            "@crate_index//:num-traits",
            "@crate_index//:serde_bytes",
        ] + extra_deps,
    )
    for (name_suffix, features, extra_deps) in [
        (
            "",
            [],
            [
                ":ledger",
                "//rs/rosetta-api/icrc1/tokens_u64",
            ],
        ),
        (
            "_u256",
            ["u256-tokens"],
            [
                ":ledger_u256",
                "//rs/rosetta-api/icrc1/tokens_u256",
            ],
        ),
        (
            "_getblocksdisabled",
            ["get-blocks-disabled"],
            [
                ":ledger",
                "//rs/rosetta-api/icrc1/tokens_u64",
            ],
        ),
        (
            "_u256_getblocksdisabled",
            [
                "get-blocks-disabled",
                "u256-tokens",
            ],
            [
                ":ledger_u256",
                "//rs/rosetta-api/icrc1/tokens_u256",
            ],
        ),
    ]
]

rust_test(
    name = "ledger_canister_test",
    crate = ":_wasm_ledger_canister",
    data = [
        ":ledger.did",
    ],
    env = {
        "CARGO_MANIFEST_DIR": "rs/rosetta-api/icrc1/ledger",
    },
    deps = ["@crate_index//:candid_parser"],
)

[
    rust_ic_test(
        name = "ledger_test" + name_suffix,
        srcs = ["tests/tests.rs"],
        crate_features = features,
        data = [
            ":block.cddl",
            ":ledger_canister" + name_suffix + ".wasm",
            "//rs/rosetta-api/icrc1/archive:archive_canister" + name_suffix + ".wasm.gz",
            "@ic-icrc1-ledger-first-version.wasm.gz//file",
            "@mainnet_ic-icrc1-ledger//file",
        ],
        env = {
            "CARGO_MANIFEST_DIR": "rs/rosetta-api/icrc1/ledger",
            "IC_ICRC1_ARCHIVE_WASM_PATH": "$(rootpath //rs/rosetta-api/icrc1/archive:archive_canister" + name_suffix + ".wasm.gz)",
            "IC_ICRC1_LEDGER_DEPLOYED_VERSION_WASM_PATH": "$(rootpath @mainnet_ic-icrc1-ledger//file)",
            "IC_ICRC1_LEDGER_FIRST_VERSION_WASM_PATH": "$(rootpath @ic-icrc1-ledger-first-version.wasm.gz//file)",
            "IC_ICRC1_LEDGER_WASM_PATH": "$(rootpath :ledger_canister" + name_suffix + ".wasm)",
        },
        deps = [
            ":ledger",
            "//packages/ic-ledger-hash-of:ic_ledger_hash_of",
            "//packages/icrc-ledger-types:icrc_ledger_types",
            "//rs/rosetta-api/icrc1",
            "//rs/rosetta-api/icrc1/ledger/sm-tests:sm-tests" + name_suffix,
            "//rs/rosetta-api/ledger_canister_core",
            "//rs/rosetta-api/ledger_core",
            "//rs/rust_canisters/dfn_http_metrics",
            "//rs/state_machine_tests",
            "//rs/test_utilities/load_wasm",
            "//rs/types/base_types",
            "@crate_index//:candid",
            "@crate_index//:cddl",
            "@crate_index//:hex",
            "@crate_index//:ic-metrics-encoder",
            "@crate_index//:leb128",
            "@crate_index//:num-traits",
            "@crate_index//:proptest",
            "@crate_index//:serde_bytes",
        ] + extra_deps,
    )
    for (name_suffix, features, extra_deps) in [
        (
            "",
            [],
            ["//rs/rosetta-api/icrc1/tokens_u64"],
        ),
        (
            "_u256",
            ["u256-tokens"],
            ["//rs/rosetta-api/icrc1/tokens_u256"],
        ),
    ]
]

rust_test(
    name = "ledger_unit_test",
    compile_data = [
        "//rs/rosetta-api/icrc1/archive:archive_canister.wasm.gz",
    ],
    crate = "ledger",
    deps = [
        ":ledger",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "//rs/rosetta-api/icrc1",
        "//rs/rosetta-api/icrc1/ledger/sm-tests",
        "//rs/rosetta-api/ledger_canister_core",
        "//rs/rosetta-api/ledger_core",
        "//rs/types/base_types",
    ],
)
