load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")
load("@rules_shell//shell:sh_test.bzl", "sh_test")
load("@bazel_skylib//rules:write_file.bzl", "write_file")

package(default_visibility = [
    "//rs:ic-os-pkg",
    "//rs/tests/node:__subpackages__",
    "//toolchains/sysimage:__pkg__",
])

filegroup(
    name = "integration_test.sh",
    srcs = ["tests/integration_test.sh"],
    visibility = ["//rs/tests/node:__subpackages__"],
)

rust_binary(
    name = "build_filesystem",
    srcs = glob(["src/**/*.rs"]),
    version = "0.1.0",
    deps = [
        # Keep sorted.
        "@crate_index//:anyhow",
        "@crate_index//:clap",
        "@crate_index//:nix",
        "@crate_index//:rand",
        "@crate_index//:regex",
        "@crate_index//:selinux",
        "@crate_index//:tar",
        "@crate_index//:tempfile",
        "@crate_index//:tokio",
        "@crate_index//:walkdir",
    ] + select({
        "@platforms//os:linux": ["//rs/ic_os/device"],
        "//conditions:default": [],
    }),
)

rust_test(
    name = "build_filesystem_test",
    crate = ":build_filesystem",
    data = ["@e2fsprogs//:mke2fs"],
    env = {
        "MKE2FS_BIN": "$(rootpath @e2fsprogs//:mke2fs)",
    },
    # The devenv has limited loop devices, more threads would cause contention and make the tests slower
    args = ["--test-threads=12"],
    # Requires root privileges to mount filesystems.
    # Marked as manual - run build_filesystem_root_test to run with sudo
    tags = [
        "manual",
        "local",
    ],
    deps = [
        "@crate_index//:proptest",
    ],
    target_compatible_with = [
        "@platforms//os:linux",
    ],
)

write_file(
    name = "sudo_wrapper",
    out = "sudo_wrapper.sh",
    content = [
        "#!/usr/bin/env bash",
        "set -euo pipefail",
        "cd /ic",
        "export HOME=/home/ubuntu",
#         "exec $(execpath :build_filesystem_test)",
        'exec bazel run --run_under="sudo -E" //rs/ic_os/build_tools/build_filesystem:build_filesystem_test',
    ],
    is_executable = True,
)

sh_test(
    name = "build_filesystem_root_test",
    srcs = [":sudo_wrapper"],
    data = [
        ":build_filesystem_test",
    ],
    # This test needs root privileges to mount filesystems
    tags = ["local"],
    target_compatible_with = [
        "@platforms//os:linux",
    ],
)
