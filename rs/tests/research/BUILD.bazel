load("//bazel:defs.bzl", "symlink_dirs")
load("//rs/tests:common.bzl", "CANISTER_HTTP_RUNTIME_DEPS", "UNIVERSAL_VM_RUNTIME_DEPS")
load("//rs/tests:system_tests.bzl", "system_test_nns")

package(default_visibility = ["//rs:system-tests-pkg"])

[
    system_test_nns(
        name = name,
        colocated_test_driver_vm_resources = {
            "vcpus": 16,
        },
        env = {
            "IC_REF_TEST_BIN": "$(rootpath //hs/spec_compliance:ic-ref-test)",
        },
        tags = ["experimental_system_test_colocation"],
        runtime_deps = UNIVERSAL_VM_RUNTIME_DEPS + CANISTER_HTTP_RUNTIME_DEPS + [
            ":ic-hs",
            "//hs/spec_compliance:ic-ref-test",
            "//ic-os/components:networking/dev-certs/canister_http_test_ca.cert",
        ],
        deps = [
            # Keep sorted.
            "//rs/registry/subnet_type",
            "//rs/tests/driver:ic-system-test-driver",
            "//rs/tests/research/spec_compliance",
            "@crate_index//:anyhow",
        ],
    )
    for name in [
        "spec_compliance_application_subnet_test",
        "spec_compliance_group_01_application_subnet_test",
        "spec_compliance_group_02_application_subnet_test",
        "spec_compliance_system_subnet_test",
        "spec_compliance_group_01_system_subnet_test",
        "spec_compliance_group_02_system_subnet_test",
    ]
]

symlink_dirs(
    name = "ic-hs",
    target_compatible_with = ["@platforms//os:linux"],
    targets = {
        "//rs/universal_canister/impl:universal_canister_no_heartbeat.wasm.gz": "test-data",
    },
    visibility = [
        "//rs:system-tests-pkg",
        "//rs/pocket_ic_server:__pkg__",
    ],
)
