load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")
load("//bazel:canisters.bzl", "rust_canister")

package(default_visibility = ["//visibility:public"])

DEPENDENCIES = [
    # Keep sorted
    "//packages/pocket-ic",
    "//rs/canister_client/sender",
    "//rs/ledger_suite/icp:icp_ledger",
    "//rs/nervous_system/agent",
    "//rs/nervous_system/clients",
    "//rs/nervous_system/common/test_keys",
    "//rs/nervous_system/integration_tests:nervous_system_integration_tests",
    "//rs/nervous_system/proto",
    "//rs/nns/common",
    "//rs/nns/constants",
    "//rs/nns/governance/api",
    "//rs/nns/sns-wasm",
    "//rs/nns/test_utils",
    "//rs/registry/transport",
    "//rs/rust_canisters/canister_test",
    "//rs/sns/cli",
    "//rs/sns/governance/api",
    "//rs/sns/swap:swap",
    "//rs/types/base_types",
    "//rs/types/management_canister_types",
    "@crate_index//:anyhow",
    "@crate_index//:candid",
    "@crate_index//:clap",
    "@crate_index//:dfx-core",
    "@crate_index//:futures",
    "@crate_index//:hex",
    "@crate_index//:ic-agent",
    "@crate_index//:ic-management-canister-types",
    "@crate_index//:k256",
    "@crate_index//:lazy_static",
    "@crate_index//:rand",
    "@crate_index//:rand_chacha",
    "@crate_index//:reqwest",
    "@crate_index//:rust_decimal",
    "@crate_index//:slog",
    "@crate_index//:tempfile",
    "@crate_index//:thiserror",
    "@crate_index//:tokio",
    "@crate_index//:url",
]

DEV_DATA = [
    # Keep sorted
    ":sns_testing_canister",
    "//rs/ledger_suite/icp/archive:ledger-archive-node-canister-wasm",
    "//rs/ledger_suite/icp/index:ic-icp-index-canister",
    "//rs/ledger_suite/icp/ledger:ledger-canister-wasm",
    "//rs/ledger_suite/icp/ledger:ledger-canister-wasm-notify-method",
    "//rs/ledger_suite/icrc1/archive:archive_canister",
    "//rs/ledger_suite/icrc1/index-ng:index_ng_canister",
    "//rs/ledger_suite/icrc1/ledger:ledger_canister",
    "//rs/nns/cmc:cycles-minting-canister",
    "//rs/nns/governance:governance-canister",
    "//rs/nns/governance:governance-canister-test",
    "//rs/nns/handlers/root/impl:root-canister",
    "//rs/nns/sns-wasm:sns-wasm-canister",
    "//rs/pocket_ic_server:pocket-ic-server",
    "//rs/registry/canister:registry-canister",
    "//rs/sns/governance:sns-governance-canister",
    "//rs/sns/root:sns-root-canister",
    "//rs/sns/swap:sns-swap-canister",
    "//rs/types/management_canister_types",
    "@cycles-ledger.wasm.gz//file",
    "@ii_dev_canister//file",
    "@nns_dapp_canister//file",
    "@sns_aggregator//file",
    "//rs/nervous_system/integration_tests:kong_backend.wasm",
    "//rs/nervous_system/integration_tests:kongswap-adaptor-canister.wasm",
    "//rs/sns/cli:sns",
]

DEV_ENV = {
    "CYCLES_MINTING_CANISTER_WASM_PATH": "$(rootpath //rs/nns/cmc:cycles-minting-canister)",
    "CYCLES_LEDGER_WASM_PATH": "$(rootpath @cycles-ledger.wasm.gz//file)",
    "GOVERNANCE_CANISTER_WASM_PATH": "$(rootpath //rs/nns/governance:governance-canister)",
    "GOVERNANCE_CANISTER_TEST_WASM_PATH": "$(rootpath //rs/nns/governance:governance-canister-test)",
    "REGISTRY_CANISTER_WASM_PATH": "$(rootpath //rs/registry/canister:registry-canister)",
    "IC_ICRC1_ARCHIVE_WASM_PATH": "$(rootpath //rs/ledger_suite/icrc1/archive:archive_canister)",
    "IC_ICRC1_INDEX_NG_WASM_PATH": "$(rootpath //rs/ledger_suite/icrc1/index-ng:index_ng_canister)",
    "IC_ICRC1_LEDGER_WASM_PATH": "$(rootpath //rs/ledger_suite/icrc1/ledger:ledger_canister)",
    "IC_ICP_INDEX_WASM_PATH": "$(rootpath //rs/ledger_suite/icp/index:ic-icp-index-canister)",
    "LEDGER_CANISTER_WASM_PATH": "$(rootpath //rs/ledger_suite/icp/ledger:ledger-canister-wasm)",
    "LEDGER_CANISTER_NOTIFY_METHOD_WASM_PATH": "$(rootpath //rs/ledger_suite/icp/ledger:ledger-canister-wasm-notify-method)",
    "LEDGER_ARCHIVE_NODE_CANISTER_WASM_PATH": "$(rootpath //rs/ledger_suite/icp/archive:ledger-archive-node-canister-wasm)",
    "SNS_WASM_CANISTER_WASM_PATH": "$(rootpath //rs/nns/sns-wasm:sns-wasm-canister)",
    "SNS_GOVERNANCE_CANISTER_WASM_PATH": "$(rootpath //rs/sns/governance:sns-governance-canister)",
    "SNS_ROOT_CANISTER_WASM_PATH": "$(rootpath //rs/sns/root:sns-root-canister)",
    "SNS_SWAP_CANISTER_WASM_PATH": "$(rootpath //rs/sns/swap:sns-swap-canister)",
    "ROOT_CANISTER_WASM_PATH": "$(rootpath //rs/nns/handlers/root/impl:root-canister)",
    "POCKET_IC_BIN": "$(rootpath //rs/pocket_ic_server:pocket-ic-server)",
    "NNS_DAPP_WASM_PATH": "$(rootpath @nns_dapp_canister//file)",
    "SNS_AGGREGATOR_WASM_PATH": "$(rootpath @sns_aggregator//file)",
    "INTERNET_IDENTITY_WASM_PATH": "$(rootpath @ii_dev_canister//file)",
    "SNS_TESTING_CANISTER_WASM_PATH": "$(rootpath :sns_testing_canister)",
    "KONG_BACKEND_CANISTER_WASM_PATH": "$(rootpath //rs/nervous_system/integration_tests:kong_backend.wasm)",
    "KONGSWAP_ADAPTOR_CANISTER_WASM_PATH": "$(rootpath //rs/nervous_system/integration_tests:kongswap-adaptor-canister.wasm)",
    "SNS_CLI_PATH": "$(rootpath //rs/sns/cli:sns)",
}

rust_binary(
    name = "sns-testing",
    testonly = True,
    srcs = ["src/bin/main.rs"],
    deps = DEPENDENCIES + [
        ":sns_testing",
    ],
)

rust_binary(
    name = "sns-testing-init",
    testonly = True,
    srcs = ["src/bin/init.rs"],
    data = DEV_DATA,
    env = DEV_ENV,
    deps = DEPENDENCIES + [
        ":sns_testing",
    ],
)

rust_library(
    name = "sns_testing",
    testonly = True,
    srcs = glob(["src/**/*.rs"]),
    crate_name = "ic_sns_testing",
    deps = DEPENDENCIES,
)

rust_test(
    name = "sns_testing_test",
    srcs = glob(["src/**/*.rs"]),
    deps = DEPENDENCIES,
)

rust_test(
    name = "sns_testing_ci",
    srcs = ["tests/sns_testing_ci.rs"],
    data = DEV_DATA,
    env = DEV_ENV,
    deps = DEPENDENCIES + [
        ":sns_testing",
    ],
)

rust_canister(
    name = "sns_testing_canister",
    testonly = True,
    srcs = ["canister/canister.rs"],
    service_file = ":canister/test.did",
    deps = [
        "@crate_index//:candid",
        "@crate_index//:ic-cdk",
        "@crate_index//:serde",
    ],
)

genrule(
    name = "_sns_testing_env",
    testonly = True,
    srcs = DEV_DATA,  # Include the dependencies to be resolved by rootpath in DEV_ENV.
    outs = ["sns_testing_env.sh"],
    cmd = "\n".join([
        'echo \'export %s="%s"\' >> $@' % (key, value)
        for key, value in DEV_ENV.items()
    ]),
)

pkg_tar(
    name = "sns_testing_env",
    testonly = True,
    srcs = ["_sns_testing_env"],
    extension = "tar",
)

pkg_tar(
    name = "sns_testing_dependencies",
    testonly = True,
    srcs = DEV_DATA,
    extension = "tar",
    strip_prefix = "/",
)

pkg_tar(
    name = "sns_testing_bin",
    testonly = True,
    srcs = [
        ":sns-testing",
        ":sns-testing-init",
        "dfx.json",
        # TODO: Add the README.md file to the tarball once if is re-written in terms of the bundle.
        # "README.md",
    ],
    extension = "tar",
)

pkg_tar(
    name = "sns_testing_bundle",
    testonly = True,
    extension = "tar.gz",
    strip_prefix = "/",
    deps = [
        ":sns_testing_bin",
        ":sns_testing_dependencies",
        ":sns_testing_env",
    ],
)
