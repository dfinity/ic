load("@rules_rust//cargo:defs.bzl", "cargo_build_script")
load("@rules_rust//rust:defs.bzl", "rust_library", "rust_test")
load("//bazel:canisters.bzl", "rust_canister")
load("//bazel:defs.bzl", "rust_test_suite_with_extra_srcs")
load("//bazel:prost.bzl", "generated_files_check")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "protos",
    srcs = glob(["proto/**/*.proto"]),
)

DEPENDENCIES = [
    # Keep sorted.
    "//packages/icrc-ledger-client:icrc_ledger_client",
    "//packages/icrc-ledger-types:icrc_ledger_types",
    "//rs/crypto/sha2",
    "//rs/nervous_system/clients",
    "//rs/nervous_system/collections/union_multi_map",
    "//rs/nervous_system/common",
    "//rs/nervous_system/common/validation",
    "//rs/nervous_system/governance",
    "//rs/nervous_system/lock",
    "//rs/nervous_system/proto",
    "//rs/nervous_system/root",
    "//rs/nervous_system/runtime",
    "//rs/nns/constants",
    "//rs/protobuf",
    "//rs/rosetta-api/icp_ledger",
    "//rs/rosetta-api/icrc1/ledger",
    "//rs/rosetta-api/ledger_core",
    "//rs/rust_canisters/canister_log",
    "//rs/rust_canisters/canister_profiler",
    "//rs/rust_canisters/dfn_candid",
    "//rs/rust_canisters/dfn_core",
    "//rs/rust_canisters/http_types",
    "//rs/sns/governance/proposal_criticality",
    "//rs/sns/governance/proposals_amount_total_limit",
    "//rs/sns/governance/token_valuation",
    "//rs/types/base_types",
    "//rs/types/management_canister_types",
    "//rs/utils",
    "@crate_index//:base64",
    "@crate_index//:build-info",
    "@crate_index//:candid",
    "@crate_index//:clap_3_2_25",
    "@crate_index//:comparable",
    "@crate_index//:hex",
    "@crate_index//:ic-metrics-encoder",
    "@crate_index//:lazy_static",
    "@crate_index//:maplit",
    "@crate_index//:num-traits",
    "@crate_index//:prost",
    "@crate_index//:rand",
    "@crate_index//:rand_chacha",
    "@crate_index//:rust_decimal",
    "@crate_index//:serde",
    "@crate_index//:serde_bytes",
    "@crate_index//:strum",
]

MACRO_DEPENDENCIES = [
    # Keep sorted.
    "//rs/nervous_system/common/build_metadata",
    "@crate_index//:async-trait",
    "@crate_index//:rust_decimal_macros",
    "@crate_index//:strum_macros",
]

BUILD_DEPENDENCIES = [
    # Keep sorted.
    "//rs/types/base_types",
    "@crate_index//:build-info-build",
    "@crate_index//:prost-build",
]

DEV_DEPENDENCIES = [
    # Keep sorted.
    "//rs/canister_client/sender",
    "//rs/nervous_system/common/test_keys",
    "//rs/nervous_system/common/test_utils",
    "//rs/sns/governance/protobuf_generator:lib",
    "//rs/sns/test_utils",
    "//rs/test_utilities/compare_dirs",
    "//rs/test_utilities/types",
    "@crate_index//:assert_matches",
    "@crate_index//:candid_parser",
    "@crate_index//:futures",
    "@crate_index//:pretty_assertions",
    "@crate_index//:proptest",
    "@crate_index//:tempfile",
    "@crate_index//:tokio",
    "@crate_index//:tokio-test",
]

MACRO_DEV_DEPENDENCIES = []

ALIASES = {}

LIB_SRCS = glob(
    ["src/**"],
    # Ensures that we do not need to rebuild just because a _test.rs file
    # changed.
    exclude = ["**/*tests.rs"],
)

cargo_build_script(
    name = "build_script",
    srcs = ["build.rs"],
    aliases = ALIASES,
    data = [],  # build script data (e.g. template files) goes here
    version = "0.9.0",
    deps = BUILD_DEPENDENCIES,
)

rust_library(
    name = "governance",
    srcs = LIB_SRCS,
    aliases = ALIASES,
    crate_name = "ic_sns_governance",
    proc_macro_deps = MACRO_DEPENDENCIES,
    version = "0.9.0",
    deps = DEPENDENCIES + [":build_script"],
)

rust_library(
    name = "governance--test_feature",
    srcs = LIB_SRCS,
    aliases = ALIASES,
    crate_features = ["test"],
    crate_name = "ic_sns_governance",
    proc_macro_deps = MACRO_DEPENDENCIES,
    version = "0.9.0",
    deps = DEPENDENCIES + [
        ":build_script",
    ],
)

rust_canister(
    name = "sns-governance-canister",
    srcs = ["canister/canister.rs"],
    aliases = ALIASES,
    compile_data = ["canister/governance.did"],
    proc_macro_deps = MACRO_DEPENDENCIES,
    service_file = ":canister/governance.did",
    deps = DEPENDENCIES + [
        ":build_script",
        ":governance",
        "//rs/nervous_system/canisters",
    ],
)

rust_canister(
    name = "sns-governance-canister-test",
    srcs = ["canister/canister.rs"],
    aliases = ALIASES,
    compile_data = ["canister/governance_test.did"],
    crate_features = ["test"],
    proc_macro_deps = MACRO_DEPENDENCIES,
    service_file = ":canister/governance_test.did",
    deps = DEPENDENCIES + [
        ":build_script",
        ":governance--test_feature",
        "//rs/nervous_system/canisters",
    ],
)

rust_test(
    name = "governance_test",
    srcs = glob(["src/**/*.rs"]),
    aliases = ALIASES,
    proc_macro_deps = MACRO_DEPENDENCIES + MACRO_DEV_DEPENDENCIES,
    deps = [":governance"] + DEPENDENCIES + DEV_DEPENDENCIES + [":build_script"],
)

rust_test(
    name = "canister_unit_test",
    srcs = glob(["canister/**/*.rs"]),
    compile_data = ["canister/governance.did"],
    crate_root = "canister/canister.rs",
    proc_macro_deps = MACRO_DEPENDENCIES,
    deps = DEPENDENCIES + DEV_DEPENDENCIES + [
        ":build_script",
        ":governance",
        "//rs/nervous_system/canisters",
    ],
)

rust_test(
    name = "canister_test_unit_test",
    srcs = glob(["canister/**/*.rs"]),
    compile_data = ["canister/governance_test.did"],
    crate_features = ["test"],
    crate_root = "canister/canister.rs",
    proc_macro_deps = MACRO_DEPENDENCIES + MACRO_DEV_DEPENDENCIES,
    deps = DEPENDENCIES + DEV_DEPENDENCIES + [
        ":build_script",
        ":governance--test_feature",
        "//rs/nervous_system/canisters",
    ],
)

rust_test_suite_with_extra_srcs(
    name = "governance_integration",
    srcs = glob(
        ["tests/**/*.rs"],
        exclude = [
            "tests/check_generated_files.rs",
            "tests/fixtures/mod.rs",
            "tests/fixtures/environment_fixture.rs",
        ],
    ),
    aliases = ALIASES,
    extra_srcs = [
        "tests/fixtures/mod.rs",
        "tests/fixtures/environment_fixture.rs",
    ],
    proc_macro_deps = MACRO_DEPENDENCIES + MACRO_DEV_DEPENDENCIES,
    deps = [":governance--test_feature"] + DEPENDENCIES + DEV_DEPENDENCIES + [":build_script"],
)

generated_files_check(
    name = "check_generated_files",
    srcs = ["tests/check_generated_files.rs"],
    data = glob(["src/gen/**"]) + [
        ":protos",
        "//rs/nervous_system/proto:protos",
        "//rs/protobuf:protos",
        "//rs/rosetta-api/icp_ledger:protos",
        "//rs/types/base_types:protos",
    ],
    manifest_dir = "rs/sns/governance",
    deps = [
        # Keep sorted.
        "//rs/sns/governance/protobuf_generator:lib",
        "//rs/test_utilities/compare_dirs",
        "@crate_index//:tempfile",
    ],
)
