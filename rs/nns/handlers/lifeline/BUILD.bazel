load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")

package(default_visibility = ["//visibility:public"])

genrule(
    name = "lifeline_actor",
    srcs = [
        "lifeline.mo",
        "//rs/nns/governance:canister/governance.did",
        "//rs/nns/handlers/root:canister/root.did",
    ],
    outs = ["lifeline.wasm"],
    cmd_bash = """
    set -e
    mkdir idl
    cat $(location //rs/nns/governance:canister/governance.did) | sed 's|service : (Governance) -> |service : |g' > idl/rrkah-fqaaa-aaaaa-aaaaq-cai.did
    cp $(location //rs/nns/handlers/root:canister/root.did) idl/r7inp-6aaaa-aaaaa-aaabq-cai.did
    $(location @build_bazel_rules_motoko_toolchain//:moc) --actor-idl idl \
      --actor-alias governance rrkah-fqaaa-aaaaa-aaaaq-cai \
      --actor-alias root r7inp-6aaaa-aaaaa-aaabq-cai \
      -o lifeline.wasm \
      -c $(location lifeline.mo)
    cp lifeline.wasm $@
""",
    exec_tools = ["@build_bazel_rules_motoko_toolchain//:moc"],
)

rust_library(
    name = "lifeline",
    srcs = ["src/lib.rs"],
    compile_data = [":lifeline.wasm"],
    rustc_env = {
        "LIFELINE_CANISTER_WASM_PATH": "$(location :lifeline.wasm)",
    },
)

rust_binary(
    name = "lifeline_bin",
    srcs = ["src/main.rs"],
    deps = [":lifeline"],
)

rust_test(
    name = "lifeline_unit_test",
    compile_data = [":lifeline.wasm"],
    crate = ":lifeline",
    deps = [
        "@wabt_rs//:wabt",
    ],
)

rust_test(
    name = "lifeline_test",
    srcs = ["tests/test.rs"],
    data = [
        "//rs/canister_sandbox",
        "//rs/canister_sandbox/sandbox_launcher",
    ],
    env = {
        "LAUNCHER_BINARY": "$(rootpath //rs/canister_sandbox/sandbox_launcher)",
        "SANDBOX_BINARY": "$(rootpath //rs/canister_sandbox)",
    },
    deps = [
        "//rs/nervous_system/common",
        "//rs/nervous_system/root",
        "//rs/nns/common",
        "//rs/nns/test_utils",
        "//rs/rust_canisters/dfn_candid",
        "//rs/types/ic00_types",
    ],
)
