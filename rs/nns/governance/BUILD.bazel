load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")
load("@rules_rust//cargo:cargo_build_script.bzl", "cargo_build_script")
load("//bazel:canisters.bzl", "rust_canister")
load("//bazel:prost.bzl", "generated_files_check")
load("//bazel:defs.bzl", "rust_bench", "rust_test_suite_with_extra_srcs")

package(default_visibility = ["//visibility:public"])

exports_files(["canister/governance.did"])

filegroup(
    name = "protos",
    srcs = glob(["proto/**"]),
)

DEPENDENCIES = [
    "//rs/crypto/getrandom_for_wasm",
    "//rs/crypto/sha",
    "//rs/monitoring/metrics_encoder",
    "//rs/nervous_system/common",
    "//rs/nns/cmc",
    "//rs/nns/common",
    "//rs/nns/constants",
    "//rs/nns/sns-wasm",
    "//rs/protobuf",
    "//rs/registry/canister",
    "//rs/rosetta-api/icp_ledger",
    "//rs/rust_canisters/dfn_candid",
    "//rs/rust_canisters/dfn_core",
    "//rs/rust_canisters/dfn_http_metrics",
    "//rs/rust_canisters/dfn_protobuf",
    "//rs/rust_canisters/on_wire",
    "//rs/sns/root",
    "//rs/sns/swap",
    "//rs/types/base_types",
    "@crate_index//:build-info",
    "@crate_index//:candid",
    "@crate_index//:comparable",
    "@crate_index//:prost",
    "@crate_index//:rand_0_8_4",
    "@crate_index//:rand_chacha_0_3_1",
    "@crate_index//:serde",
    "@crate_index//:strum",
] + select({
    "@rules_rust//rust/platform:wasm32-unknown-unknown": [],
    "//conditions:default": [
        "@crate_index//:csv",
        "//rs/nervous_system/common/test_keys",
    ],
})

MACRO_DEPENDENCIES = [
    "//rs/nervous_system/common/build_metadata",
    "@crate_index//:async-trait",
    "@crate_index//:strum_macros",
]

BUILD_DEPENDENCIES = [
    "@crate_index//:build-info-build",
]

DEV_DEPENDENCIES = [
    "//rs/config",
    "//rs/nns/governance/protobuf_generator:lib",
    "//rs/sns/swap/protobuf_generator:lib",
    "//rs/test_utilities/compare_dirs",
    "@crate_index//:futures",
    "@crate_index//:lazy_static",
    "@crate_index//:pretty_assertions",
    "@crate_index//:proptest",
    "@crate_index//:tempfile",
    "@crate_index//:tokio",
    "@crate_index//:tokio-test",
] + select({
    "@rules_rust//rust/platform:wasm32-unknown-unknown": [],
    "//conditions:default": [
        "@crate_index//:assert_matches",
        "@crate_index//:maplit",
        "//rs/types/types",
    ],
})

MACRO_DEV_DEPENDENCIES = []

ALIASES = {}

cargo_build_script(
    name = "build_script",
    srcs = ["build.rs"],
    aliases = ALIASES,
    data = [],  # build script data (e.g. template files) goes here
    version = "0.8.0",
    deps = BUILD_DEPENDENCIES,
)

rust_library(
    name = "governance",
    srcs = glob([
        "src/**/*.rs",
        "gen/**/*.rs",
    ]),
    aliases = ALIASES,
    crate_name = "ic_nns_governance",
    proc_macro_deps = MACRO_DEPENDENCIES,
    version = "0.8.0",
    deps = DEPENDENCIES + [
        ":build_script",
    ],
)

rust_library(
    name = "governance-test",
    srcs = glob([
        "src/**/*.rs",
        "gen/**/*.rs",
    ]),
    aliases = ALIASES,
    crate_features = ["test"],
    crate_name = "ic_nns_governance",
    proc_macro_deps = MACRO_DEPENDENCIES,
    version = "0.8.0",
    deps = DEPENDENCIES + [
        ":build_script",
        "//rs/sns/swap:swap_test_feature",
    ],
)

rust_binary(
    name = "generate_did",
    srcs = ["canister/canister.rs"],
    compile_data = ["canister/governance.did"],
    proc_macro_deps = MACRO_DEPENDENCIES,
    deps = DEPENDENCIES + [
        ":build_script",
        ":governance",
    ],
)

rust_canister(
    name = "governance-canister",
    srcs = ["canister/canister.rs"],
    aliases = ALIASES,
    compile_data = ["canister/governance.did"],
    proc_macro_deps = MACRO_DEPENDENCIES,
    deps = DEPENDENCIES + [
        ":build_script",
        ":governance",
    ],
)

rust_canister(
    name = "governance-canister-test",
    srcs = ["canister/canister.rs"],
    aliases = ALIASES,
    compile_data = ["canister/governance.did"],
    crate_features = ["test"],
    proc_macro_deps = MACRO_DEPENDENCIES,
    deps = DEPENDENCIES + [
        ":governance-test",
        ":build_script",
    ],
)

generated_files_check(
    name = "check_generated_files",
    srcs = ["tests/check_generated_files.rs"],
    data = glob(["gen/**/*.rs"]) + [
        ":protos",
        "//rs/types/base_types:protos",
        "//rs/nns/common:protos",
        "//rs/rosetta-api/icp_ledger:protos",
        "//rs/sns/root:protos",
        "//rs/sns/swap:protos",
    ],
    manifest_dir = "rs/nns/governance",
    deps = [
        "//rs/nns/governance/protobuf_generator:lib",
        "//rs/test_utilities/compare_dirs",
        "@crate_index//:tempfile",
    ],
)

rust_test(
    name = "canister_unit_test",
    crate = ":_wasm_governance-canister",
    data = ["canister/governance.did"],
    env = {
        "CARGO_MANIFEST_DIR": "rs/nns/governance",
    },
)

rust_test(
    name = "governance_test",
    aliases = ALIASES,
    crate = ":governance",
    proc_macro_deps = MACRO_DEPENDENCIES + MACRO_DEV_DEPENDENCIES,
    deps = DEPENDENCIES + DEV_DEPENDENCIES + [
        ":build_script",
    ],
)

rust_bench(
    name = "scale_bench",
    srcs = ["benches/scale.rs"],
    proc_macro_deps = ["@crate_index//:async-trait"],
    deps = [
        ":governance",
        "//rs/nervous_system/common",
        "//rs/nns/common",
        "//rs/rosetta-api/icp_ledger",
        "//rs/types/base_types",
        "@crate_index//:criterion",
        "@crate_index//:futures",
        "@crate_index//:tokio-test",
    ],
)

rust_test_suite_with_extra_srcs(
    name = "governance_integration_test",
    srcs = glob(
        ["tests/*.rs"],
        exclude = [
            # fake.rs is a module dependency, not a test
            "tests/fake.rs",
            # check generated files is handed in the generated_files_check rule above
            "tests/check_generated_files.rs",
        ],
    ),
    aliases = ALIASES,
    crate_features = ["test"],
    data = ["tests/neurons.csv"],
    env = {
        "NEURON_CSV_PATH": "rs/nns/governance/tests/neurons.csv",
    },
    extra_srcs = glob([
        "tests/*/*.rs",
    ]) + ["tests/fake.rs"],
    proc_macro_deps = MACRO_DEPENDENCIES + MACRO_DEV_DEPENDENCIES,
    deps = [":governance-test"] + DEPENDENCIES + DEV_DEPENDENCIES + [
        "//rs/sns/swap:swap_test_feature",
        ":build_script",
    ],
)
